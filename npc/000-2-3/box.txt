// Evol scripts.
// Author:
//    Reid
// Description:
//    Captain Nard dialogs.
//    Nard does the introduction with a small quest.
//    This quest is meant to teach the player how to use a weapon,and to bring food to the ship.
//    4 bits array:
//    ShipQuests
// Variable:
//    ShipQuests_Nard
// Values:
//    0   Introduction of the boxes and Nard. This is displayed when the player never spoke to Nard or his box.
//    1   Nard spoke and gave access to the outdoor of the ship.
//    2   Completed the Gugli quest.
//    3   ChefGado Quest accepted.
//    4   ChefGado Quest completed and "Introduction" chapter finalized.
//    5   Reward taken from the box.

000-2-3.gat,25,24,0	script	Box	102;1,{
    set @q, getq(ShipQuests_Nard);

    mesn "Narrator";
    mes col(l("Some Bandanas and Sailor Hats are inside this box."), 9);
    next;
    mes col(l("What do you wish to do?"), 9);
    next;

    menu
        l("Take a Bandana."), -,
        l("Nothing."), L_Quit;

    if (@q == 0) goto L_NoQuest;
    if (@q == 4) goto L_Give;
    if (@q > 4) goto L_Already;

    setcamnpc "Nard";
    mes "";
    mesn "Nard";
    mesq l("Please don't touch these hats, they are for crew members only.");

    close;

L_NoQuest:
    mes "";
    mesn "Narrator";
    mes col(l("Nard looks surprised and stops you."), 9);
    next;

    setcamnpc "Nard";
    mesn "Nard";
    mesq l("You like these hats, right?");
    next;
    mesq l("How about I ask you to help the crew? It would mean that you're one of us and that you will be able to get one of these hats.");
    next;
    mesq l("We need as many hands as possible to explore the island out there, and to get some new food.");
    next;
    mesq l("You could meet some of the other sailors this way, and... Getting this hat of course, will be a sign of you becoming part of our crew.");
    next;
    mesq l("What do you think?");
    next;

    menu
        l("Why not, I've got plenty of free time."), -,
        l("I think that I'm still a bit sick."), L_Quit;

    mes "";
    mesn "Nard";
    mesq l("Great!");
    next;
    mesq l("I give you this key, it opens any doors on this ship.");
    next;
    mesq l("Now go outside and talk with Gugli, he'll tell you what we need.");
    next;

    callfunc "InventoryPlace", 718, 1;
    setq ShipQuests_Nard, 1;
    getitem "JohanneKey", 1;

    close;

L_Give:
    setcamnpc "Nard";
    mes "";
    mesn "Nard";
    mesq l("You are now part of the crew. Thanks again for your help.");

    callfunc "InventoryPlace", 2900, 1;

    setq ShipQuests_Nard, 5;
    getitem "Bandana", 1;
//  Need to add a skill for the crew at this line.

    close;

L_Already:
    setcamnpc "Nard";
    mes "";
    mesn "Nard";
    mesq l("You already took a Bandana, please put this one back in the box.");

    close;

L_Quit:
    closedialog;
    close;
}
