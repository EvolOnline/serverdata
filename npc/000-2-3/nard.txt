// Evol scripts.
// Author:
//    Reid
// Description:
//    Captain Nard dialogs.
//    Nard does the introduction with a small quest.
//    This quest is meant to teach the player how to use a weapon,and to bring food to the ship.
// 2 bits array:
//    ShipQuests
// Variable:
//    ShipQuests_NardBox
// Values: 
//    0   Introduction of the box and Nard. This is displayed when the player never spoke to Nard or his box.
//    1   Nard spoke and gave access to the outdoor of the ship.
//    2   Completed the quest.
//    3   Opened the chest and finished the introduction.

000-2-3.gat,25,26,0,1	script	Nard	306;5,{

    set @q, getq(ShipQuests_Nardbox);

l_Checker:
    if (@q == 1) goto l_NotYet;
    if (@q == 2) goto l_Give;
    if (@q > 2) goto l_Already;

    mesn;
    mesq l("Hello."); 
    next;
    mesq l("Let me introduce myself, I am Nard, captain of this ship."); 
    next;
    mesq lg("It is nice to see that you woke up fine at the same time. Elmo came here to tell me this good news!");
    next;

    mesn "Elmo";
    mesq l("Oh... Err, yes I did, or, well, good day to you!");
    next;

    mesn;
    mesq l("Hehehe, he is a bit nervous, please forgive him, it is not everyday we have a new member in the crew!");
    next;
    mesq l("So, how do you feel? I see that Julia did a marvelous job! You look like you're in good health now.");
    next;

l_Menu:
    menu
        lg("I feel ok."), l_Ok,
        l("Who's this Julia?"), l_Julia,
        lg("I'm a bit sick..."), -;

    mes "";
    mesn;
    mesq l("Oh, I was going to ask you if you wanted to help the crew search for some food and explore the island out there.");
    next;

l_MenuQuest:
    menu
        l("What do you need?"), -,
        l("Is there a reward?"), l_NeedHead;

    mes "";
    mesn;
    mesq l("Oh! I like that sort of answer!");
    next;
    mesq g(l("We need manpower on the island... Oh forgive me, women are also welcome, of course."),
           l("We need manpower on the island.")); 
    next;

    goto l_NeedContent;

l_NeedHead:
    mes "";
    mesn;

l_NeedContent:
    mesq l("Sure, there is a reward for your task."); 
    next;
    mesq l("Our crew is like a family, and if you agree to help us, I would like to invite you to join our family!"); 
    next;
    mesq l("What? It's not a good reward?"); 
    next;
    mesq l("Oh well. I'll also give you one of these hats from the box around you, but only after you complete your task!"); 
    next;
    mesq l("Go outside and talk with Gugli, he'll tell you what we need.");
    next;

    setq ShipQuests_Nardbox, 1;

    close;

l_Ok:
    mes "";
    mesn;
    mesq l("Good to know.");
    next;
    mesq l("We made a stop at a little island, before making it to the port of Artis.");
    next;
    mesq l("It would be good for you to do some exercise, the ship isn't big enough for that.");
    next;

    goto l_SpecialTask;

l_Julia:
    mes "";
    mesn;
    mesq l("You have an awful amnesia.");
    next;
    mesq l("She is the nurse and shipkeeper of this ship, and also an excellent chef!");
    next;
    mesq lg("But most important, she is the one who took care of you when you were unconscious.");
    next;

l_SpecialTask:
    mesq l("I know that you are just starting to feel better, but I'd like to give you a special task.");
    next;

    goto l_MenuQuest;

l_NotYet:
    mesn;
    mesq l("You still haven't completed your task.");  
    close;

l_Give:
    mesn;
    mesq l("Congrats!"); 
    next;
    mesq l("You are now part of the crew! Thanks again for your help.");

    setq ShipQuests_Nardbox, 3;
    getitem "Bandana", 1;
//  Need to add a skill for the crew at this line.

    close;

l_Already:
    mesn;
    mesq l("I don't need any help right now, come back later."); 
    close;

}
