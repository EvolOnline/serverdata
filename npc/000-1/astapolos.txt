// Evol scripts.
// Authors:
//    Hal9000
//    Reid
// Description:
//    Sailor part of the Gugli's quest.
//    Croconut box.

000-1.gat,22,50,0,1	script	Astapolos	107;2,{

    set @g, getq(ShipQuests_Gugli);
    if (@g == 0) goto l_NoTask;

    set @q, getq(ShipQuests_Astapolos);
    if (@q == 1) goto l_CallFunc;

    mesn;
    mesq l("So finally someone has came to visit me?");
    next;

    menu
        l("In fact, I came here to work on my task."), -;

    mes "";
    mesn;
    mesq l("Oh, I see. That's right. Why should someone come here to see me, seriously?");
    next;
    mesq l("Do you think this is fair? They sent me down here alone, far away from everyone.");
    next;
    
    menu
        l("I'm sorry for your situation. They should alternate you with another sailor sometime."), -,
        l("Well if you are whining like that all the time, then I think there is a reason behind their choice."), l_Whyning;

    mes "";
    mesn;
    mesq l("Exactly. Even our task is ridiculous. Why should we collect food down here if we have plenty of those tasty and soft rattos inside our ship, waiting just to be hunted and cooked?");
    next;

    menu
        l("You... You eat... rattos? I think you're just a fool."), l_Whyning,
        l("Oh... eating rattos. Sounds... ehm... Delicious, yes."), -;

    mes "";
    mesn;
    mesq l("You like them, don't you? Just speak with Peter then. He is on the second deck of the ship. He will send you to downstairs where you will find our furry meal!");
    next;
    mesq l("Oh, but you were not here to talk about that I guess.");
    next;
    

    menu
        l("No, indeed. I was here to help you with those boxes. Can I bring one of them back to Gugli?"), -;
    
    mes "";
    mesn;
    mesq l("Usually I don't allow anyone to touch my boxes but...");
    next;
    mesq l("You have been so nice with me. So, please, take one.");
    next;
    
    setq ShipQuests_Astapolos, 1;
    getitem "CroconutBox", 1;
    
    mes "";
    mesn;
    mesq l("Good luck! And don't forget to visit Peter and our little friends when you will be back onboard!");
    close;


l_Whyning:
    mes "";
    mesn;
    mesq l("Then just leave me alone.");
    close;

l_CallFunc:
    callfunc "SailorFood";

    menu  
        rif(getq(ShipQuests_Astapolos) == 1, l("I'm still busy, I need to find the other sailors.")), -,
        l("How are things going?"), l_HowAreYou,
        l("Who are you?"), l_Who;

    mes "";
    mesn;
    mesq l("Who are you looking for?");
    next;

    menu
        rif(getq(ShipQuests_Ale) == 0, l("Ale.")), l_Top,
        rif(getq(ShipQuests_Gulukan) == 0, l("Gulukan.")), l_Bottom,
        rif(getq(ShipQuests_Jalad) == 0, l("Jalad.")), l_Top,
        rif(getq(ShipQuests_QMuller) == 0, l("QMuller.")), l_Bottom,
        rif(getq(ShipQuests_Tibbo) == 0, l("Tibbo.")), l_Bottom,
        l("Nobody."), -;

    close;

l_HowAreYou:

    mes "";
    mesn;
    mesq l("A sunny and hot day,");
    next;
    mesq l("a quiet place,");
    next;
    mesq l("a ground!");
    next;
    mesq l("I hope that answers your question.") + " " + l("Hehe!");

    close;

l_Who:
    mes "";
    mesn;
    mesq l("My name is Astapolos. Q'Muller and I joined Nard's crew a few years ago when it was just a little merchant ship.");
    next;
    mesq l("At this time, we were selling crab food on our old mushroom island.");
    next;
    mesq l("And now I'm a sailor, as you can see!");

    close;

l_Bottom:
    mes "";
    mesn;
    mesq l("I've seen him at the bottom of the island, check around.");

    close;

l_Top:
    mes "";
    mesn;
    mesq l("I've seen him at the top of the island.");
    next;
    mesq l("You should walk to the north.");

    close;

l_NoTask:
    mes "";
    mesn;
    mesq l("Just leave me alone.");
    close;

}
