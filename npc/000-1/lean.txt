// Evol scripts.
// Authors:
//    Hal9000, Reid
// Description:
//    Sailor who gives some informations to the player.

000-1.gat,79,108,0,1	script	Lean	106;2,{

    set @gugli, getq(ShipQuests_Gugli);

    mesn;
    mesq l("Hey")+ " " + strcharinfo(0) + "!"; 
    next;
    mesq l("I hope you don't mind that we used your raft to build this ramp.");
    next;
    mesq l("We usually don't stop in such splendid places, but the captain lets us stay here while he writes down the location of this new island on his map!");
    next;
    mesq l("Some sailors are looking for goods we could trade in our next destination.");
    next;

    menu
        rif(@gugli == 1, l("Well, I was in fact looking for them. Where are they now?")), l_Sailors,
        l("Wait... Where are we going?"), l_Artis,
        l("Hey, how do you know my name?"), l_Name;

l_Name:
    mes "";
    mesn;
    mesq l("A few moments ago, I heard your conversation with Darlin.");
    next;
    mesq l("Oh, not everything, don't worry, but your name came up at least twice in the conversation.");

    close;

l_Artis:
    mes "";
    mesn;
    mesq l("Artis of course!");
    next;
    mesq l("It's where every merchant ship end their road and we won't be an exception.");
    next;
    mesq l("If you're looking for us there, mostly all of us will be at Pedro's restaurant and in the Inn for Silvio's case...");

    close;

l_Sailors:
    mes "";
    mesn;
    mesq l("They are all around the island.");
    next;
    mesq l("I'm sure Tibbo is alone in the south-west corner of the island. He believes it's the best place to collect @@s.", getitemlink("Aquada"));
    next;
    mesq l("While Jalad and Ale like to work together, as they use to do on the ship, last time I've seen them, they were near the big lake, north from here.");
    next;
    mesq l("They shouldn't be too far from each other.");
    next;

    menu
        l("Thank you for your help."), l_Quit,
        l("Gugli mentioned six sailors. What about the others?"), -;

    mes "";
    mesn;
    mesq l("I'm not sure. They probably have left the ship early this morning. I was not awake yet.");
    next;
    mesq l("Try to ask Max. He's an early riser.");

    close;

l_Quit:
    close;

}
