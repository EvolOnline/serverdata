// Evol scripts.
// Authors:
//    Hal9000, Reid
// Description:
//    Friend of Sapartan, he gives some information on how to find Gulukan, Astapolos and Q-Muller.

000-1.gat,72,110,0,1	script	Max	105;2,{
    set @gugli, getq(ShipQuests_Gugli);
    set @knife, getq(ShipQuests_Knife);

    mesn;
    mesq lg("We are glad captain Nard has let you join us down here!");
    next;
    mesq l("Living inside a ship is great, but we sometimes need some fresh air.");
    next;
    mesq l("Anyway, can I help you in any way?");
    next;

    menu
        rif(@gugli == 0, l("Well, is there anything I can do here to help?")), l_Help,
        rif(@gugli == 1, lg("Gugli asked me to help sailors with their food gatherings. Do you know where could I find them?")), l_Sailors,
        rif(@gugli > 1, l("Seems I've helped everyone here. I don't know what to do now!")), l_Annoyed,
        rif(@knife == 0, lg("It looks like you are an expert of the life inside 'La Johanne'. Do you know any hidden secrets nobody knows?")), l_Secrets,
        l("No thanks, not at the moment."), l_Quit;

l_Help:
    mes "";
    mesn;
    mesq l("You could start talking to Gugli. He always has tasks for people willing to help");

    close;

l_Sailors:
    mes "";
    mesn;
    mesq l("I remember I saw few of them leaving the ship early this morning to get to work.");
    next;
    mesq l("I recall seeing Gulukan, Astapolos and Q'Muller.");
    next;

    goto l_MenuSailors;

l_MenuSailors:
    menu
        l("Do you know where can I find Gulukan?"), l_Gulukan,
        l("Any clue on where Astapolos may be hidden?"), l_Astapolos,
        l("What about Q'Muller? Where is he?"), l_Qmuller,
        l("Alright. I will go looking for them now."), l_Quit;

l_Gulukan:
    mes "";
    mesn;
    mesq l("Oh, he is not far away from here. Just take the road through the crocojungle north from here.");
    next;
    mesq l("You will find a wooden sign near a crossroad. He is a few steps on the left.");
    next;

    goto l_MenuSailors;

l_Astapolos:
    mes "";
    mesn;
    mesq l("Astapolos... That guy is shy like a piou!");
    next;
    mesq l("He likes to hide near the little lake in the north-west side of the island.");
    next;

    goto l_MenuSailors;

l_Qmuller:
    mes "";
    mesn;
    mesq l("You need to cross the crocojungle heading north.");
    next;
    mesq l("You can spot Q'Muller on a cliff on the west busy collecting @@s.", getitemlink("Plushroom"));
    next;

    goto l_MenuSailors;

l_Annoyed:
    mes "";
    mesn;
    mesq l("There is always something you can do here, like killing some tortugas or help Peter clean the ship from rattos.");

    close;

l_Secrets:
    mes "";
    mesn;
    mesq l("No there are no secrets hidden below the ship's wood. Some sailors swear they have seen people hidden somewhere near the stow. Just stories sailors love to tell...");
    next;
    mesq l("Oh! And in one of the room of the second deck you can find Gado's knives. Our chef keeps there the sharpest ones... Probably not just to cut some carrots...");
    next;
    mesq l("Go and grab one of them. A good knife will help you hunting the creatures of this island.");

    close;

l_Quit:
    close;
}
