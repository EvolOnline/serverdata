// Evol scripts.
// Authors:
//    Hal9000
//    Reid
// Description:
//    Sailor part of the Gugli's quest.
//    Aquada box.

000-1.gat,34,111,0,1	script	Tibbo	110;2,{
    set @g, getq(ShipQuests_Gugli);
    if (@g == 0) goto l_NoTask;

    set @q, getq(ShipQuests_Tibbo);
    if (@q == 1) goto l_CallFunc;

    mesn;
    mesq l("Uh? Hi! I'm sorry, but I have no time for chatting.");
    next;

    menu
        l("Fine... I was just going to give you some help..."), -;

    mes "";
    mesn;
    mesq l("Hel... What?! Wait!");
    next;
    mesq l("What kind of help?");
    next;

    menu
        lg("Gugli asked me to help you carrying one of these boxes you are filling."), -;

    mes "";
    mesn;
    mesq l("Then it seems I have to apologize.");
    next;
    mesq l("I am surprised someone has been sent here for help. It is quite... unusual.");
    next;
    mesq l("Bring this box to Gugli. He will be extremely happy!");
    next;

    set @item, 714;
    set @count, 1;
    callfunc "InventoryPlace", @item, @count;

    setq ShipQuests_Tibbo, 1;
    getitem "AquadaBox", 1;

    mes "";
    mesn;
    mesq l("Thank you and be careful with that box. I have worked really hard to fill it!");
    close;

l_CallFunc:
    callfunc "SailorFood";

    menu
        rif(getq(ShipQuests_Gugli) > 1, l("Task is done.")), l_TaskDone,
        rif(getq(ShipQuests_Tibbo) == 1 && (getq(ShipQuests_Ale) == 0 || getq(ShipQuests_Astapolos) == 0 || getq(ShipQuests_Gulukan) == 0 || getq(ShipQuests_Jalad) == 0 || getq(ShipQuests_QMuller) == 0), l("I'm still busy, I need to find the other sailors.")), -,
        l("What's that food?"), l_Aquada;

    mes "";
    mesn;
    mesq l("Who are you looking for?");
    next;

    menu
        rif(getq(ShipQuests_Ale) == 0, l("Ale.")), l_Top,
        rif(getq(ShipQuests_Astapolos) == 0, l("Astapolos.")), l_Top,
        rif(getq(ShipQuests_Gulukan) == 0, l("Gulukan.")), l_Top,
        rif(getq(ShipQuests_Jalad) == 0, l("Jalad.")), l_Top,
        rif(getq(ShipQuests_QMuller) == 0, l("Q'Muller.")), l_Top,
        l("Nobody."), -;

    closedialog;
    close;

l_Top:
    mes "";
    mesn;
    mesq l("I've seen him at the top of the island.");
    next;
    mesq l("You should walk to the north to find him.");

    close;

l_Aquada:
    mes "";
    mesn;
    mesq l("This is an @@, a light blue sea fruit. They're highly desired in the archipelago.", getitemlink("Aquada"));
    next;
    mesq l("You can find them in the ocean, near corals and other sea beings. But the sea is too heavy today, you shouldn't try swimming for now.");
    next;
    mesq l("Or else, if you really wish one, and if you feel capable, you can try killing tortugas or crocs if you're strong enough. I'm sure they could drop one or two @@s.", getitemlink("Aquada"));

    close;

l_NoTask:
    mes "";
    mesn;
    mesq l("I'm sorry but I have no time to chat with you.");
    close;

l_TaskDone:
    mes "";
    mesn;
    mesq l("Awesome!");

    close;
}
