// Evol scripts.
// Authors:
//    Hal9000
//    Reid
// Description:
//    Sailor part of the Gugli quest.
//    Plushroom box.

000-1.gat,46,35,0,1	script	Jalad	112;2,{
    set @g, getq(ShipQuests_Gugli);
    if (@g == 0) goto l_NoTask;

    set @q, getq(ShipQuests_Jalad);
    if (@q == 1) goto l_CallFunc;

    mesn;
    mesq l("*Uff*, *Argh*, why don't you give me some help?");
    next;
    mesq l("I'm not suited for this kind of work!");
    next;

    menu
        l("I was going to ask you if you would need any help."), l_Continue,
        l("What exactly is your real job?"), -;

    mes "";
    mesn;
    mesq l("I'm the cook's assistant of the ship. I help Gado, La Johanne's Chef, in his daily work.");
    next;
    mesq l("You should go see him. He is one of the most experienced sailors we have.");
    next;

    menu
        l("I will surely visit him. Do you need any help with your boxes?"), -;

l_Continue:
    mes "";
    mesn;
    mesq l("Surely. Take this box full of @@s.", getitemlink("Plushroom"));
    next;

    set @item, 716;
    set @count, 1;
    callfunc "InventoryPlace", @item, @count;

    setq ShipQuests_Jalad, 1;
    getitem "PlushroomBox", 1;

    mes "";
    mesn;
    mesq l("They are so tasty when cooked together with some @@. Don't drop any of them!", getitemlink("PiouLegs"));

    close;

l_CallFunc:
    callfunc "SailorFood";

    menu
        rif(getq(ShipQuests_Gugli) > 1, l("Task is done.")), l_TaskDone,
        rif(getq(ShipQuests_Jalad) == 1 && (getq(ShipQuests_Ale) == 0 || getq(ShipQuests_Astapolos) == 0 || getq(ShipQuests_Gulukan) == 0 || getq(ShipQuests_QMuller) == 0 || getq(ShipQuests_Tibbo) == 0), l("I'm still busy, I need to find the other sailors.")), -,
        l("What's that?"), l_Plushroom;

    mes "";
    mesn;
    mesq l("Who are you looking for?");
    next;

    menu
        rif(getq(ShipQuests_Ale) == 0, l("Ale.")), l_Ale,
        rif(getq(ShipQuests_Astapolos) == 0, l("Astapolos.")), l_Bottom,
        rif(getq(ShipQuests_Gulukan) == 0, l("Gulukan.")), l_Bottom,
        rif(getq(ShipQuests_QMuller) == 0, l("Q'Muller.")), l_Bottom,
        rif(getq(ShipQuests_Tibbo) == 0, l("Tibbo.")), l_Bottom,
        l("Nobody."), -;

    closedialog;
    close;

l_Bottom:
    mes "";
    mesn;
    mesq l("I've seen him at the bottom of the island, have a look around.");

    close;

l_Ale:
    setcamnpc "Ale";

    next;
    mesn;
    mesq l("Look, there he is!");
    next;

    restorecam;
    close;

l_Plushroom:
    mes "";
    mesn;
    mesq l("'That', as you call it, is a @@. There're plenty on this island!", getitemlink("Plushroom"));
    next;
    mesq l("It's a kind of mushroom. We call it like that because of it's taste, just like a marshmallow. It also has this name because of it's appearance. It looks like a plush!");
    next;
    mesq l("There are some groups of these @@s all around this island. Just pick some and have a try.", getitemlink("Plushroom"));

    close;

l_NoTask:
    mes "";
    mesn;
    mesq l("Sometimes, I wish somebody is sent here to help us.");

    close;

l_TaskDone:
    mes "";
    mesn;
    mesq l("Perfect!");

    close;
}
