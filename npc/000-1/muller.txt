// Evol scripts.
// Authors:
//    Hal9000
//    Reid
// Description:
//    Sailor part of the Gugli's quest.
//    Plushroom box.

000-1.gat,35,69,0,1	script	Q'Muller	104;2,{

    set @g, getq(ShipQuests_Gugli);
    if (@g == 0) goto l_NoTask;

    set @q, getq(ShipQuests_QMuller);
    if (@q == 1) goto l_CallFunc;

    mesn;
    mesq l("Can't you see I'm working?!");
    next;

    menu
        l("Well I'm working too! Don't you want me to carry one of these boxes back to Gugli?"), -;

    mes "";
    mesn;
    mesq l("Oh I see... Sailors are not able to do their job anymore, is that it?!");
    next;
    
    menu
        l("Indeed! Goodbye."), close,
        l("Yes, it is true! You are not able at all. Nard has been waiting for the food for too long already. Hurry up, you lazy sailors!"), -;

    mes "";
    mesn;
    mesq l("Are you blind? Do you really think these boxes are light and soft as a piou's feather?!");
    next;

    menu
        l("I am sure I can run with one of them on my shoulder."), -;

    mes "";
    mesn;
    mesq l("Well then... Take this one!");
    next;

    setq ShipQuests_QMuller, 1;
    getitem "PlushroomBox", 1;

    mes "";
    mesn;
    mesq l("And now I want to see you run!");
    close;

l_CallFunc:
    callfunc "SailorFood";

    menu
        rif(getq(ShipQuests_QMuller) == 1, l("I'm still busy, I need to find the other sailors.")), -,
        l("What's that?"), l_Plushroom;

    mes "";
    mesn;
    mesq l("Who are you looking for?");
    next;

    menu
        rif(getq(ShipQuests_Ale) == 0, l("Ale.")), l_Top,
        rif(getq(ShipQuests_Astapolos) == 0, l("Astapolos.")), l_Top,
        rif(getq(ShipQuests_Jalad) == 0, l("Jalad.")), l_Top,
        rif(getq(ShipQuests_Gulukan) == 0, l("Gulukan.")), l_Bottom,
        rif(getq(ShipQuests_Tibbo) == 0, l("Tibbo.")), l_Bottom,
        l("Nobody."), -;

    close;

l_Bottom:
    mes "";
    mesn;
    mesq l("I've seen him at the bottom of the island, check around.");

    close;

l_Top:
    mes "";
    mesn;
    mesq l("I've seen him at the top of the island.");
    next;
    mesq l("You should walk to the north.");
    close;

l_Plushroom:
    mes "";
    mesn;
    mesq l("'That', as you call it, is a @@. There're plenty on this island!", getitemlink("Plushroom"));
    next;
    mesq l("It's a kind of mushroom. We call it like that because of it's taste, just like a marshmallow. It also has this name because of it's appearance, it looks like a plush!");
    next;
    mesq l("There are few groups of these @@s all around this island. Just pick some and have a try.", getitemlink("Plushroom"));

    close;

l_NoTask:
    mes "";
    mesn;
    mesq l("You see? I'm working here!");
    close;

}
