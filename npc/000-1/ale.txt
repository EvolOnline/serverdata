// Evol scripts.
// Author:
//    Reid
// Description:
//    Sailor part of the Gugli's quest.
//    Aquada box.

000-1.gat,55,23,0,1	script	Ale	105;2,{

    set @q, getq(ShipQuests_Ale);
    if (@q == 1) goto l_CallFunc;

    mesn;
    mesq l("Can't you see I'm working?!");
    next;

    menu
        l("Well I'm working too! Don't you want me to carry one of those boxes back to Gugli?"), -;

    mes "";
    mesn;
    mesq l("Ooh... It seems sailors are not able to do their job anymore?!");
    next;
    
    menu
        l("Fine! Goodbye."), l_Close;
        l("Yes, it is true! You are not able at all. Nard has been waiting for the food for too long already. Hurry up, you lazy sailors!"), -;

    mes "";
    mesn;
    mesq l("Are you blind? Do you think these boxes are light and soft as a Piou's feather?!");
    next;

    menu
        l("I am sure I can run with one of them on my shoulder."), -;

    mes "";
    mesn;
    mesq l("Well then... Take this one!");
    next;
    
    setq ShipQuests_Ale, 1;
    getitem "AquadaBox", 1;

    mes "";
    mesn;
    mesq l("And now I want to see you run!");
    close;

l_CallFunc:
    callfunc "SailorFood";

    menu
        rif(getq(ShipQuests_Ale) == 1, l("I'm still busy, I need to find the other sailors.")), -,
        l("What's this fruit?"), l_Aquada;

    mes "";
    mesn;
    mesq l("Who are you looking for?");
    next;

    menu
        rif(getq(ShipQuests_Astapolos) == 0, l("Astapolos.")), l_Bottom,
        rif(getq(ShipQuests_Gulukan) == 0, l("Gulukan.")), l_Bottom,
        rif(getq(ShipQuests_Jalad) == 0, l("Jalad.")), l_Jalad,
        rif(getq(ShipQuests_QMuller) == 0, l("QMuller.")), l_Bottom,
        rif(getq(ShipQuests_Tibbo) == 0, l("Tibbo.")), l_Bottom,
        l("Nobody."), -;

    close;

l_Bottom:
    mes "";
    mesn;
    mesq l("I've seen him on the bottom of the island, ask Jalad for more information.");

    close;

l_Jalad:
    setcamnpc "Jalad";

    next;
    mesn;
    mesq l("Look, he's there!");
    next;

    restorecam;
    close;

l_Aquada:
    mes "";
    mesn;
    mesq l("This is an @@, a light blue sea fruit. They're highly desired in the archipelago.", getitemlink("Aquada"));
    next;
    mesq l("You can find them in the ocean, near corals and other sea beings. But the sea is too heavy today, you shouldn't try to swim there.");
    next;
    mesq l("Orelse, if you really wish one, and if you feel capable, you can try killing tortugas or crocs if you're strong enough. I'm sure they could drop one or two @@s.", getitemlink("Aquada"));

    close;
    
l_Close:

    close;

}
