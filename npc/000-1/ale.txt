// Evol scripts.
// Author:
//    Reid
// Description:
//    Sailor part of the Gugli's quest.
//    Aquada box.

000-1.gat,55,23,0,1	script	Ale	105;2,{
    set @g, getq(ShipQuests_Gugli);
    if (@g == 0) goto l_NoTask;

    set @q, getq(ShipQuests_Ale);
    if (@q == 1) goto l_CallFunc;

    mesn;
    mesq l("Ouch...");
    next;
    mesq l("These heavy boxes are killing me, I don't feel my arms anymore!");
    next;

    menu
        lg("Gugli sent me down here to help you."), -,
        l("What's that?"), l_Plushroom,
        l("See you aboard."), l_Quit;

    mes "";
    mesn;
    mesq l("Really? That's kind of you, I accept your help!");
    next;
    mesq l("Hmm... Let's see, take this one.");
    next;

    set @item, 714;
    set @count, 1;
    callfunc "InventoryPlace", @item, @count;

    setq ShipQuests_Ale, 1;
    getitem "AquadaBox", 1;

    mes "";
    mesn;
    mesq l("I will take care of the other ones don't worry.");

    close;

l_CallFunc:
    callfunc "SailorFood";

    menu
        rif(getq(ShipQuests_Gugli) > 1, l("Task is done.")), l_TaskDone,
        rif(getq(ShipQuests_Ale) == 1 && (getq(ShipQuests_Astapolos) == 0 || getq(ShipQuests_Gulukan) == 0 || getq(ShipQuests_Jalad) == 0 || getq(ShipQuests_QMuller) == 0 || getq(ShipQuests_Tibbo) == 0), l("I'm still busy, I need to find the other sailors.")), -,
        l("What's that?"), l_Plushroom;

    mes "";
    mesn;
    mesq l("Who are you looking for?");
    next;

    menu
        rif(getq(ShipQuests_Astapolos) == 0, l("Astapolos.")), l_Bottom,
        rif(getq(ShipQuests_Gulukan) == 0, l("Gulukan.")), l_Bottom,
        rif(getq(ShipQuests_Jalad) == 0, l("Jalad.")), l_Jalad,
        rif(getq(ShipQuests_QMuller) == 0, l("Q'Muller.")), l_Bottom,
        rif(getq(ShipQuests_Tibbo) == 0, l("Tibbo.")), l_Bottom,
        l("Nobody."), l_Quit;

l_Bottom:
    mes "";
    mesn;
    mesq l("I've seen him on the bottom of the island, ask Jalad for more information.");

    close;

l_Jalad:
    setcamnpc "Jalad";

    next;
    mesn;
    mesq l("Look, here he is!");
    next;

    restorecam;

    close;

l_Plushroom:
    mes "";
    mesn;
    mesq l("'That', as you call it, is a @@. There're plenty on this island!", getitemlink("Plushroom"));
    next;
    mesq l("It's a kind of mushroom. We call it like that because of it's taste, just like a marshmallow. It also has this name because of it's appearance, it looks like a plush!");
    next;
    mesq l("There are few groups of these @@s all around this island. Just pick some and have a try.", getitemlink("Plushroom"));

    close;

l_NoTask:
    mes "";
    mesn;
    mesq l("Ouch... These boxes are so heavy!");

    close;

l_TaskDone:
    mes "";
    mesn;
    mesq l("That's good to hear!");

    close;

l_Quit:
    closedialog;
    close;
}
