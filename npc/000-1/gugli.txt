// Evol scripts.
// Authors:
//    Hal9000
//    Reid
// Description:
//    Elmo's twin
//    Charged of an important quest from Nard, he asks you to help other sailors to keep their box to the ship.
// Variable:
//    0   ShipQuests_Couwan
//    1   ShipQuests_Nard
//    2   ShipQuests_Gugli
// Values:
//    00   Never talked with Couwan.
//    01   Spoke, and received the quest scam.
//    02   Done quest scam.
//    10   Introduction of the box and Nard. This is displayed when the player never spoke to Nard or his box.
//    11   Nard spoke and gave access to the outdoor of the ship.
//    12   Completed the quest.
//    13   Opened the chest and finished the introduction.
//    20   Never talked with Gugli.
//    21   Gugli gives you the task.
//    22   Gave all of the box to Gugli.

000-1.gat,85,108,0,1	script	Gugli	313;2,{

OnTalk:
    mesn;
    set @nard, getq(ShipQuests_Nard);
    set @gugli, getq(ShipQuests_Gugli);
    set @gado, getq(ShipQuests_ChefGado);

    if (@nard > 3) goto l_AllComplete;
    if (@nard > 1 || @gugli == 2) goto l_TaskCompleted;
    if (@gugli == 1) goto l_TaskGiven;

    mesq lg("Hi @@, I heard the captain sent you down here, uh?", strcharinfo(0));
    next;
    mesq l("An unexpected help is always welcome!");
    next;
    mesq l("As you may know, we are long distance travelers and our whole crew needs to store enough food to survive. You don't know how dangerous the sea life can be.");
    next;

    menu
        l("Actually, I have an in-depth knowledge of this type of life."), -;

    mes "";
    mesn;
    mesq lg("Well, even though you have been rescued by us, it doesn't make you an expert sailor, am I right?");
    next;
    mesq l("But let's talk about this island. We're docking here to find some fine quality goods.");
    next;
    mesq l("We usually dock around these small pieces of land because they provide us some of the best food of all the archipelago.");
    next;
    mesq l("At the moment, our crew is busy collecting @@s, @@s and @@s.", getitemlink("Croconut"), getitemlink("Plushroom"), getitemlink("Aquada"));
    next;

    menu
        l("So, what can I do for you?"), -;

    mes "";
    mesn;
    mesq l("Right! Sailors are pretty quick with their job. They just take too much time bringing all the food back to the ship. Maybe you can help them.");
    next;
    mesq l("The captain wants:");
    mesq l("- 2 @@", getitemlink("AquadaBox"));
    mesq l("- 2 @@", getitemlink("CroconutBox"));
    mesq l("- 2 @@", getitemlink("PlushroomBox"));
    next;
    mesq l("Ask sailors around here they shouldn't be so far away.");
    next;

    setq ShipQuests_Gugli, 1;
    close;

l_TaskGiven:
    mesq l("Great to see you! What can I do for you today?");
    next;

    if (getq(ShipQuests_Ale) == 1 && getq(ShipQuests_Astapolos) == 1 && getq(ShipQuests_Gulukan) == 1 && getq(ShipQuests_Jalad) == 1 && getq(ShipQuests_QMuller) == 1 && getq(ShipQuests_Tibbo) == 1) goto l_MenuDone;

l_Menu:
    menu
        rif(getq(ShipQuests_Couwan) == 1, l("Couwan gave me this box, it is for you.")), l_Couwan,
        rif(countitem("AquadaBox") > 0 || countitem("CroconutBox") > 0 || countitem("PlushroomBox") > 0, l("I have some food for you.")), l_CollectSmthing,
        rif(getq(ShipQuests_Gugli) < 2, l("Who should I search for?")), l_SailorNames,
        rif(getq(ShipQuests_Gugli) < 2, l("Where can I find your crew?")), l_Location,
        l("Bye!"), l_Quit;

l_MenuDone:
    menu
        rif(getq(ShipQuests_Couwan) == 1, l("Couwan gave me this box, it is for you.")), l_Couwan,
        l("I have collected all the boxes you needed."), l_TaskDone,
        l("See you!"), l_Quit;

l_SailorNames:
    mes "";
    mesn;
    mesq l("Ale and Tibbo should bring 2 boxes of @@s, Astapolos and Gulukan 2 of @@s and Jalad and Q'Muller were charged of the boxes of @@s.", getitemlink("Aquada"), getitemlink("Croconut"), getitemlink("Plushroom"));
    next;
    mesq l("Do you need any other information?");
    next;

    goto l_Menu;

l_CollectSmthing:
    mes "";
    mesn;
    mesq l("Good job!");
    next;

    if (countitem("AquadaBox") == 2) delitem "AquadaBox", 2;
    if (countitem("CroconutBox") == 2) delitem "CroconutBox", 2;
    if (countitem("PlushroomBox") == 2) delitem "PlushroomBox", 2;
    if (countitem("AquadaBox") == 1) delitem "AquadaBox", 1;
    if (countitem("CroconutBox") == 1) delitem "CroconutBox", 1;
    if (countitem("PlushroomBox") == 1) delitem "PlushroomBox", 1;

    mesq l("You still need to give me boxes from: ");
    if (getq(ShipQuests_Astapolos) == 0), mesq l("- Astapolos");
    if (getq(ShipQuests_Ale) == 0), mesq l("- Ale");
    if (getq(ShipQuests_Gulukan) == 0), mesq l("- Gulukan");
    if (getq(ShipQuests_Jalad) == 0), mesq l("- Jalad");
    if (getq(ShipQuests_QMuller) == 0), mesq l("- Q'Muller");
    if (getq(ShipQuests_Tibbo) == 0), mesq l("- Tibbo");

    close;

l_Couwan:
    mes "";
    mesn;
    mesq l("What?! This tritan is the worse shirker I ever met!");
    next;
    mesq l("Well, thanks for the box. But... He was supposed to bring it back to me himself. I never asked him to give it to you!");
    next;
    mesq l("Take these coins in exchange and be careful.");
    next;
    mesq l("Not everybody has a kind mind. Scammers can be anywhere, even among us!");
    next;
    mesq l("May this be a lesson for you.");

    if (countitem("FishBox") > 0) delitem "FishBox", 1;
    setq ShipQuests_Couwan, 2;
    set zeny, zeny+10;
    message strcharinfo(0), l("You receive @@ E!", 10);

    close;

l_Location:
    mes "";
    mesn;
    mesq l("There're 6 sailors who will need your help. They're all around the island.");
    next;
    mesq l("I can't really help you find them since I've been checking the... Hmm... Landscape, since morning...");
    next;
    mesq l("You can try talking with some other sailors to get some information about that.");
    next;

    set @r, rand(3000)/1000;
    if (@r == 1) goto l_Silvio;
    if (@r == 2) goto l_Lean;

    setcamnpc "Sapartan";
    mesq l("Max and Sapartan for example?");
    next;

    restorecam;
    close;

l_Silvio:
    setcamnpc "Silvio";
    mesq l("Silvio for example?");
    next;

    restorecam;
    close;

l_Lean:
    setcamnpc "Lean";
    mesq l("Lean for example?");
    next;

    restorecam;
    close;

l_TaskDone:
    if (countitem("AquadaBox") == 2) delitem "AquadaBox", 2;
    if (countitem("CroconutBox") == 2) delitem "CroconutBox", 2;
    if (countitem("PlushroomBox") == 2) delitem "PlushroomBox", 2;
    if (countitem("AquadaBox") == 1) delitem "AquadaBox", 1;
    if (countitem("CroconutBox") == 1) delitem "CroconutBox", 1;
    if (countitem("PlushroomBox") == 1) delitem "PlushroomBox", 1;
    setq ShipQuests_Gugli, 2;

    getexp 50, 0;

    mes "";
    mesn;
    mesq l("Well done! The ship is now ready to sail again!");
    next;
    mesq l("You should inform Nard about the loading progress of the food on to the boat. He will be pleased to hear that good news!");

    close;

l_TaskCompleted:
    mesq l("You are now part of the crew... At least for us down here!");
    next;
    mesq l("You should inform Nard about the loading progress of the food on to the boat. He will be pleased to hear that good news!");
    next;
    mesq l("Tell him that everything went fine and that we're almost done with the food.");
    next;

    goto l_Menu;

l_AllComplete:
    mesq l("Elmo told me what Nard said, congrats!");
    next;
    mesq g(l("You're one of us now, that's great! I was sure that you were a kind lady when I first saw you!"),
           l("You're one of us now, that's great! I was sure that you were a good man when I first saw you!"));
    next;

    goto l_Menu;

l_Quit:
    closedialog;
    close;

}

000-1.gat,83,107,0,1	script	GugliBarrierCheck	0,2,0,{
    set @gugli, getq(ShipQuests_Gugli);
    if (@gugli > 0) close;
    doevent "Gugli::OnTalk";
    close;
}
