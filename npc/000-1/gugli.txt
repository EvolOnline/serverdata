// Evol scripts.
// Authors:
//    Hal9000
//    Reid
// Description:
//    Elmo's twin
//    Charged of an important quest from Nard, he asks you to help other sailors to keep their box to the ship.
// Variable:
//    0   ShipQuests_Couwan
//    1   ShipQuests_Nard
//    2   ShipQuests_Gugli
// Values: 
//    00   Never talked with Couwan.
//    01   Spoke, and received the quest scam.
//    02   Done quest scam.
//    10   Introduction of the box and Nard. This is displayed when the player never spoke to Nard or his box.
//    11   Nard spoke and gave access to the outdoor of the ship.
//    12   Completed the quest.
//    13   Opened the chest and finished the introduction.
//    20   Never talked with Gugli.
//    21   Gugli gives you the task.
//    22   Gave all of the box to Gugli.

000-1.gat,85,108,0,1	script	Gugli	313;2,{
OnTalk:
    mesn;
    set @n, getq(ShipQuests_Nard);
    set @g, getq(ShipQuests_Gugli);
    if (@n > 1 || @g == 2) goto l_TaskCompleted;
    if (@g == 1) goto l_TaskGiven;

    mesq l("Hi @@, I have heard captain sent you down here, uh?", strcharinfo(0));  
    next;
    mesq l("An unexpected help is always welcome.");
    next;
    mesq l("As you may know we are long-distance traveller, and our crew need to stock enough food to survive. You don't know how dangerous sea life can be.");
    next;

    menu
        l("Actually I have an in-depth knowledge of this life."), -;

    mes "";
    mesn;
    mesq l("Well have been rescued by us doesn't make you an expert sailor, am I right?");
    next;
    mesq l("But let's talk more about this island, we're docking here to find some fine quality foods.");
    next;
    mesq l("We usually dock around these small piece of land because they provide us some of the finest food of all the archipelago.");
    next;
    mesq l("So at the moment, our crew is busy collecting croconuts, plushrooms and aquada.");
    next;

    menu
        l("So what can I do for you?"), -;

    mes "";
    mesn;
    mesq l("Right! Sailors are pretty quick with their job. They just take too much time bringing all the food back to the ship. Maybe you can help them.");
    next;
    mesq l("Captain wants 2 boxes of Croconouts, 2 of Plushroom and 2 of Aquada.");
    next;
    mesq l("Ask sailors around here they shouldn't be so far.");
    next;

    setq ShipQuests_Gugli, 1;
    goto l_Menu;

l_TaskGiven:
    mesq l("Great to see you! What can I do for you today?");
    next;

l_Menu:
    menu
        rif(getq(ShipQuests_Couwan) == 1, l("Couwan gave me this box, it is for you.")), l_Couwan,
        rif(countitem("AquadaBox") > 1 && countitem("CroconutBox") > 1 && countitem("PlushroomBox") > 1, l("I have collected all the boxes you needed.")), l_TaskDone,
        l("Where can I find your crew?"), l_Location,
        l("See you."), -;

    close;

l_Couwan:
    mes "";
    mesn;
    mesq l("What? This tritan is the worse shirker I ever met.");
    next;
    mesq l("Well, thanks for the box. But... he was supposed to bring it back to me himself. I never asked him to give it to you!");
    next;
    mesq l("Take these coins in exchange and be careful.");
    next;
    mesq l("Not everybody has a kind mind. Scammers can be anywhere, even among us!");
    next;
    mesq l("May this be a lesson for you.");

    setq ShipQuests_Couwan, 2;
    set zeny, zeny+50;
    if (countitem("FishBox") > 0) delitem "FishBox", 1;
    message strcharinfo(0), l("You receive 50GP!");
    close;

l_Location:
    mes "";
    mesn;
    mesq l("There're 6 sailors, they're all around the island.");
    next;
    mesq l("I can't really help you to find them myself, I've been checking the... Hmm... Landscape, since the the morning...");
    next;
    mesq l("You can try to talk with some other sailors to get more information about this.");
    next;

    set @r, rand(3000)/1000;
    if (@r == 1) goto l_Silvio;
    if (@r == 2) goto l_Lean;

    setcamnpc "Sapartan";
    mesq l("Max and Sapartan for example?");
    next;

    restorecam;
    close;

l_Silvio:
    setcamnpc "Silvio";
    mesq l("Silvio for example?");
    next;

    restorecam;
    close;

l_Lean:
    setcamnpc "Lean";
    mesq l("Lean for example?");
    next;

    restorecam;
    close;

l_TaskDone:
    if (countitem("AquadaBox") > 1) delitem "AquadaBox", 2;
    if (countitem("CroconutBox") > 1) delitem "CroconutBox", 2;
    if (countitem("PlushroomBox") > 1) delitem "PlushroomBox", 2;
    setq ShipQuests_Gugli, 2;

    mesq l("Well done! Now the ship is ready to sail again!");
    next;
    mesq l("You should inform Nard about the progress of the load of the merchandise. He will be enthusiastic.");

    close;

l_TaskCompleted:
    mesq l("You are now part of this crew... At least for us down here!");
    next;
    mesq l("You should inform Nard about the progress of the load of the merchandise. He will be enthusiastic.");
    next;
    mesq l("Say him that everything went fine, and that we're almost done with the foods.");

    close;

}

000-1.gat,83,107,0,1	script	GugliBarrierCheck	0,2,0,{
    set @q, getq(ShipQuests_Gugli);
    if (@q > 0) close;
    doevent "Gugli::OnTalk";
    close;

}
