// Evol scripts.
// Authors:
//    Reid
// Description:
//    Darlin discussion from 10meter tall mat.
// Variable:
//    ShipQuests_Darlin
// Values: 
//    0   Never speak
//    1   Speak+Quest

000-1.gat,25,26,0,1	script	Darlin	103,{

    mesn;
    set @q, geta(ShipQuests, ShipQuests_Darlin);
    if (@q > 0) goto l_QuestGiven;

    mesq "Hey! Ehoo!!"; 
    next;
    mesq "Do you hear me??";
    next;

    menu
        l("Yes!"), -,
        l("No!"), l_Quit;

    mes "";
    mesn;
    mesq l("Good!!");
    next;
    mesq l("I was here when they rescued you!!");
    next;
    mesq l("Do you feel better??");
    next;

    menu
        l("I feel better!"), -,
        l("Why don't you come down to talk?"), l_GoDown;

    mes "";
    menu
        l("And you, how are you going?"), -,
        l("But I need to go, bye!"), l_Bye;
// Et lÃ 

    mes "";
    mesn;
    mesq l("I'm going fine!!");
    next;
    mesq g(l("So, what's your name lady??"), l("What's your name dude??"));
    next;

    menu
         strcharinfo(0) + ".", -,
         l("Maybe you can come down to talk?"), -;

    mes "";
    mesn;
    mesq l("What did you said??");
    next;
    mesq l("I don't understand!!");
    next;

    menu
        l("I said see you later!!"), l_Bye,
        l("I said... Why don't you come down to talk??"), l_GoDown,
        l("I'm called") + " " + strcharinfo(0) + "!", -;

    mes "";
    mesn;
    mesq strcharinfo(0) + "?? " + l("It's a nice name!!");
    next;
    mesq l("So what's up?? What are you doing??");
    next;

l_Menu:
    menu
        l("Maybe you can come down to talk?"), l_GoDown,
        l("I search Gugli, where is he?"), -,
        l("Nothing, but I need to go!"), l_Bye;

    setcamnpc "Gugli";
    mes "";
    mesn;
    mesq l("Oh look there!!");
    next;
    mesq l("He is just in bottom of the ship, you can't miss him!!");
    restorecam;

    close;

l_GoDown:
    mes "";
    mesn;
    mesq l("I would like!!");
    next;
    mesq l("But I can't, I need to keep an eye on the sea, to warn the crew if there is some pirats around!!");
    next;
    mesq l("I also check if there are some dangerous animals around...");
    next;

    menu
        l("Do you seen a danger around?"), -,
        l("Interesting... I let you at your task then!"), l_Bye;

    mes "";
    mesn;

    if (@q > 0) goto l_DangerAround;
    seta ShipQuests, ShipQuests_Darlin, 1;

    mesq l("Oh no, but I've noticed a weird light on the other edge of this island, I wonder what it can be...");
    next;
    mesq l("Maybe you can look at this??");
    next;

    menu
        l("I will check this!"), l_Bye,
        l("Ok, but Gugli need my help first."), -;

    mes "";
    mesn;
    mesq l("It's ok, crew task are much important than curiousity!!");
    next;
    mesq l("Anyway, maybe while searching some food with Gugli you will get the time to look at this!!");
    close;

l_Bye:
    mes "";
    mesn;
    mesq l("Fine, bye!!");

l_Quit:
    close;

l_QuestGiven:
    mesq l("Ehoo") + " " + strcharinfo(0) + "!!"; 
    next;
    mesq l("Did you find what was this light?!");
    next;

    menu
        l("I forgot where it was."), -,
        l("Oh, it was nothing important!"), l_Nothing,
        l("Yes, it's a chest!"), l_Chest;

    mes "";
    mesn;
    mesq l("On the edge of this island!!");
    next;
    mesq l("You need to walk to the north!!");

    goto l_Menu;

l_Nothing:
    mes "";
    mesn;
    mesq l("Oh, ok!!");

    goto l_Menu;

l_Chest:
    mes "";
    mesn;
    mesq l("Excuse me, but what?? Speak higher!!");
    next;
    mesq l("If you find something then it's good!!");

    goto l_Menu;

l_DangerAround:
    mesq l("Nothing else than sharks and this weird light!!");
 
    close;

}