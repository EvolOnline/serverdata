// Evol scripts.
// Authors:
//    Reid
// Description:
//    Darlin discussion from 10meter tall mat.
// Variable:
//    ShipQuests_Darlin
// Values: 
//    0   Never speak
//    1   Speak+Quest

000-1.gat,25,26,0,1	script	Darlin	103,{

    mesn;
    set @q, geta(ShipQuests, ShipQuests_Darlin);
    if (@q > 0) goto l_QuestGiven;

    mesq l("HEY! EHOO!"); 
    next;
    mesq l("DO YOU HEAR ME?");
    next;

    menu
        l("Yes!"), -,
        l("No!"), l_Quit;

    mes "";
    mesn;
    mesq l("GOOD!");
    next;
    mesq l("I WAS HERE WHEN THEY RESCUED YOU!");
    next;
    mesq l("DO YOU FEEL BETTER?");
    next;

    menu
        l("I feel better!"), -,
        l("Why don't you come down to talk?"), l_GoDown;

    mes "";
    menu
        l("And you, how are you doing?"), -,
        l("But I need to go, bye!"), l_Bye;

    mes "";
    mesn;
    mesq l("I'M DOING FINE!");
    next;
    mesq l("SO, WHAT'S YOUR NAME?");
    next;

    menu
         strcharinfo(0) + ".", -,
         l("Maybe you can come down to talk?"), -;

    mes "";
    mesn;
    mesq l("WHAT DID YOU SAID?");
    next;
    mesq l("I DON'T UNDERSTAND!");
    next;

    menu
        l("I SAID SEE YOU LATER!"), l_Bye,
        l("I said... WHY DON'T YOU COME DOWN TO TALK?"), l_GoDown,
        l("I'm called") + " " + strcharinfo(0) + "!", -;

    mes "";
    mesn;
    mesq strcharinfo(0) + "? " + l("THAT'S A NICE NAME!");
    next;
    mesq l("SO WHAT'S UP? WHAT ARE YOU DOING?");
    next;

l_Menu:
    menu
        l("Maybe you can come down to talk?"), l_GoDown,
        l("I'm looking for Gugli, where is he?"), -,
        l("Nothing, I need to go!"), l_Bye;

    setcamnpc "Gugli";
    mes "";
    mesn;
    mesq l("OH LOOK THERE!");
    next;
    mesq l("HE'S RIGHT IN THE BOTTOM OF THE SHIP, YOU CAN'T MISS HIM!!");
    restorecam;

    close;

l_GoDown:
    mes "";
    mesn;
    mesq l("I WOULD LOVE TO!");
    next;
    mesq l("BUT I CAN'T, I NEED TO KEEP AN EYE ON THE SEA, TO WARN THE CREW IF THERE HAPPEN TO BE SOME PIRATES AROUND!");
    next;
    mesq l("I ALSO WATCH FOR DANGEROUS ANIMALS...");
    next;

    menu
        l("Have you seen anything dangerous?"), -,
        l("Interesting... I'll leave you to your task then!"), l_Bye;

    mes "";
    mesn;

    if (@q > 0) goto l_DangerAround;
    seta ShipQuests, ShipQuests_Darlin, 1;

    mesq l("Oh no, but I've noticed a weird light on the other edge of this island, I wonder what it can be...");
    next;
    mesq l("MAYBE YOU CAN LOOK AT THIS?");
    next;

    menu
        l("I'll look at it."), l_Bye,
        l("Ok, but Gugli needs my help first."), -;

    mes "";
    mesn;
    mesq l("IT'S OK, CREW TASKS ARE MUCH MORE IMPORTANT THAN CURIOSITY!");
    next;
    mesq l("ANYWAY, MAYBE WHEN YOU ARE LOOKING FOR FOOD WITH GUGLI YOU'LL HAVE THE TIME TO LOOK AT THIS!");
    close;

l_Bye:
    mes "";
    mesn;
    mesq l("FINE, BYE!");

l_Quit:
    close;

l_QuestGiven:
    mesq l("EHOO") + " " + strcharinfo(0) + "!"; 
    next;
    mesq l("DID YOU FIND OUT WHAT THE LIGHT IS?");
    next;

    menu
        l("I forgot where it was."), -,
        l("Yes, it's a chest."), l_Chest,
        l("Oh, it was nothing important!"), l_Nothing;

    mes "";
    mesn;
    mesq l("ON THE TOP OF THE CLIFF!");
    next;
    mesq l("YOU NEED TO WALK NORTH!");

    goto l_Menu;

l_Nothing:
    mes "";
    mesn;
    mesq l("OH, OK!");

    goto l_Menu;

l_Chest:
    mes "";
    mesn;
    mesq l("EXCUSE ME, BUT WHAT DID YOU SAY? SPEAK LOUDER!");
    next;
    mesq l("IF YOU FIND SOMETHING THEN IT'S GOOD!");

    goto l_Menu;

l_DangerAround:
    mesq l("NOTHING ELSE OTHER THAN SHARKS AND AN ODD LIGHT.");
 
    close;

}