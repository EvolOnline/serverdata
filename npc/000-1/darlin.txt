// Evol scripts.
// Author:
//    Reid
// Description:
//    Darlin talks from the top of the tall mast.
// Variable:
//    x   ShipQuests_Darlin
// Values: 
//    0   Never talked with Darlin.
//    1   Spoke, and received quest.
//    2   Opened the treasure chest.
//    3   Get back to Darlin.

000-1.gat,89,94,0,1	script	Darlin	103;14,{

    mesn;
    set @q, getq(ShipQuests_Darlin);
    if (@q == 1 || @q == 2) goto OnQuestGiven;
    if (@q > 2) goto l_QuestDone;
OnStart:
    mesq l("HEY! HEY YOU!"); 
    next;
    mesq l("CAN YOU HEAR ME?");
    next;

    menu
        l("Yes!"), -,
        l("No!"), l_Quit;

    mes "";
    mesn;
    mesq l("GOOD!");
    next;
    mesq l("I WAS HERE WHEN THEY RESCUED YOU!");
    next;
    mesq l("DO YOU FEEL BETTER?");
    next;

    menu
        l("I do feel better!"), -,
        l("Why don't you come down to talk?"), l_GoDown;

    mes "";
    menu
        l("And you, how are you doing?"), -,
        l("But I need to go, see you!"), l_Bye;

    mes "";
    mesn;
    mesq l("I AM DOING FINE!");
    next;
    mesq l("SO, WHAT IS YOUR NAME?");
    next;

    menu
         strcharinfo(0) + ".", -,
         l("Maybe you can come down to talk?"), -;

    mes "";
    mesn;
    mesq l("WHAT DID YOU SAY?");
    next;
    mesq l("I DO NOT UNDERSTAND!");
    next;

    menu
        l("I said... WHY DON'T YOU COME DOWN TO TALK?"), l_GoDown,
        l("I SAID, SEE YOU LATER!"), l_Bye,
        l("I'M CALLED, @@!", strcharinfo(0)), -;

    mes "";
    mesn;
    mesq strcharinfo(0) + "? " + l("THAT'S A NICE NAME!");
    next;
    mesq l("SO WHAT'S UP? WHAT ARE YOU DOING?");
    next;

l_Menu:
    set @q, getq(ShipQuests_Darlin);

    menu
        l("Maybe you can come down to talk?"), l_GoDown,
        l("I'm looking for Gugli, where is he?"), -,
        l("I need to go, sorry."), l_Bye;

    setcamnpc "Gugli";
    mes "";
    mesn;
    mesq l("OH, LOOK THERE!");
    next;
    mesq l("HE IS AT THE BOTTOM OF THE SHIP, DOWN THE STAIRS. YOU CAN'T MISS HIM!");
    restorecam;

    close;

l_GoDown:
    mes "";
    mesn;
    mesq l("I WOULD LOVE TO!");
    next;
    mesq l("BUT I CAN'T, I NEED TO KEEP AN EYE ON THE SEA, TO WARN THE CREW IF THERE HAPPEN TO BE SOME PIRATES AROUND!");
    next;
    mesq l("I ALSO WATCH FOR DANGEROUS ANIMALS...");
    next;

    menu
        l("Have you seen anything dangerous?"), -,
        l("Interesting... I'll leave you to your task then!"), l_Bye;

    mes "";
    mesn;

    if (@q > 0) goto l_DangerAround;
    setq ShipQuests_Darlin, 1;

    mesq l("Oh no, but I've noticed a weird light on the other edge of this island, I wonder what it can be...");
    next;
    mesq l("MAYBE YOU CAN LOOK AT THIS?");
    next;

    menu
        l("I'll look at it."), l_Bye,
        l("Sure, but Gugli needs my help first."), -;

    mes "";
    mesn;
    mesq l("IT'S OK, CREW TASKS ARE MUCH MORE IMPORTANT THAN CURIOSITY!");
    next;
    mesq l("ANYWAY, MAYBE WHEN YOU ARE LOOKING FOR FOOD WITH GUGLI YOU WILL HAVE THE TIME TO LOOK AT THIS!");
    close;

l_Bye:
    mes "";
    mesn;
    mesq l("FINE, BYE!");

l_Quit:
    close;

OnQuestGiven:
    mesq l("HEY @@!", strcharinfo(0));
    next;
    mesq l("DID YOU FIND OUT WHAT THE LIGHT WAS?");
    next;

    menu
        rif(@q == 1, l("I forgot where it was...")), -,
        rif(@q == 2, l("Yes, it's a chest.")), l_Chest,
        l("Oh, nothing important."), l_Nothing;

    mes "";
    mesn;
    mesq l("ON THE TOP OF THE CLIFF!");
    next;
    mesq l("YOU NEED TO WALK NORTH!");

    goto l_Menu;

l_Nothing:
    if (@q == 2) setq ShipQuests_Darlin, 3;
    mes "";
    mesn;
    mesq l("OH, OK!");

    goto l_Menu;

l_Chest:
    mes "";
    mesn;
    mesq l("SORRY, BUT WHAT DID YOU SAY? SPEAK LOUDER!");
    next;
    mesq l("IF YOU FIND SOMETHING, THEN IT'S GREAT!");

    setq ShipQuests_Darlin, 3;
    goto l_Menu;

l_DangerAround:
    mesq l("NOTHING ELSE OTHER THAN SHARKS AND AN ODD LIGHT.");
 
    close;

l_QuestDone:
    mesq l("HEY") + " " + strcharinfo(0) + "!"; 
    next;

    goto l_Menu;

}
