// Evol scripts.
// Author:
//    Hal9000
// Description:
//    La Johanne Chef.
// Variable:
//    ShipQuests_ChefGado
//    ShipQuests_Nard
// Values:
//    0   Default, no quest given.
//    1   Quest accepted.
//    2   Ingredients collected, ready to poison Julia.
//    3   Julia poisoned.
//    4   Quest complete.
//    5   Quest aborted. Julia wins.

000-2-1.gat,17,28,0,1	script	Chef Gado	318;2,{

    set @q, getq(ShipQuests_ChefGado);
    set @n, getq(ShipQuests_Nard);
    if (@q == 1) goto l_QuestAccepted;
    if (@q == 2) goto l_PoisonAccepted;
    if (@q == 3) goto l_PoisonJulia;
    if (@q == 4) goto l_QuestComplete;
    if (@q == 5) goto l_QuestAborted;
    if (@n == 3) goto l_QuestStart;

    mesn;
    mesq l("Where is the damn salt!? Give me the salt. I know you have it!");
    close;

l_QuestStart:
    mesn;
    mesq l("So it seems I have another stomach to fill. Those bastards have no respect for who feeds them every damn day!");
    next;

    menu
        l("So rude. What is the reason behind your malice?"), l_Rude,
        l("I swear, I do not eat so much."), -;

    close;

l_Rude:
    mes "";
    mesq l("UAHAHAHAAH! I am the cook of that bunch of donkeys, I can be rude with everyone from the bossy captain to that harpy-lady!");
    next;

    menu
        l("Harpy-lady?"), -;

    mes "";
    mesq l("I am sure you know her. Nice dress, large smile and a constant flux of orders and rules. Julia they call her!");
    next;
    mesq l("You have to know that there was a time during which I was the unopposed lieutenant of this crew. Then one day one of the damn sailor brought here that nice lady.");
    next;
    mesq l("I will not describe you all the details but after one month she was ruling the ship and I was sent down here cooking for those traitors!");
    next;
    mesq l("But I swear that one day..ooh my tasty revenge. Hhmm, actually I wonder if...maybe...");
    next;

    menu
        l("M... Maybe?"), -;

    mes "";
    mesq l("... Welcome onboard cutie! I just realized I need delicious ingredients to prepare a... a special recipe.");
    next;
    mesq l("I only need 1 @@, 1 @@ and...", getitemlink("PiouLegs"), getitemlink("Croconut"));
    next;
    mesq l("... and 1 @@.", getitemlink("Aquada"));
    next;

    menu
        l("Sure, why not?"), l_Sure,
        l("Aquada!? I will not help you with your evil plan!"), -;

    close;

l_Sure:
    setq ShipQuests_ChefGado, 1;
    mes "";
    mesq l("Well done! Collect all the ingredients and then come back here. You will be rewarded once they understand who rules this ship!");

    close;

l_QuestAccepted:
    mesn;
    mesq l("Have you collected all the ingredients for my recipe? The special one too?");
    next;

    if (countitem("PiouLegs") > 0 && countitem("Croconut") > 0 && countitem("Aquada") > 0) goto l_Collected;

    menu
        l("Not yet. I will be back soon."), -;

    close;

l_Collected:
    menu
        l("All your... fresh ingredients are ready to be cooked."), -;

    delitem "PiouLegs", 1;
    delitem "Croconut", 1;
    delitem "Aquada", 1;
    setq ShipQuests_ChefGado, 2;

    mes "";
    mesq l("Let me see... Crispy legs, disgusting liquids... Let's start!");
    next;
    mesq l("Water, salt, spicy herbs and meat stuffed with my special surprise!");
    next;
    mesq l("Done. Here, take it! Now here is the plan. Go talk with her and offer our beautiful lady a bite of her arrogance!");
    getitem "PoisonedDish", 1;

    close;

l_PoisonAccepted:
    mesn;
    mesq l("Fool! Just come here when you have finished our little... secret mission.");

    close;

l_PoisonJulia:
    mesn;
    mesq l("The usurper has been punished! This is a great day, take this reward as a prize for your loyalty to the old commander!.");

    setq ShipQuests_ChefGado, 4;

    close;

l_QuestComplete:
    mesn;
    mesq l("Oh, it's you. I think it is better we do not talk for a while. They suspect something.");

    close;

l_QuestAborted:
    mesn;
    mesq l("You dirty liar. I will add you on the traitors list.");

    close;

}
