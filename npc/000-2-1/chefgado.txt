// Evol scripts.
// Author:
//    Hal9000
// Description:
//    La Johanne Chef.
// Variable:
//    ShipQuests_ChefGado
// Values:
//    0   Default, no quest given.
//    1   Quest accepted.
//    2   Ingredients collected, ready to poison Julia.
//    3   Quest accomplished.

000-2-1.gat,17,28,0,1	script	Chef Gado	318;2,{

    set @q, getq(ShipQuests_ChefGado);
    if (@q == 1) goto OnQuestAccepted;
    if (@q == 2) goto OnPoisonAccepted;

    mesn;
    mesq l("So it seems I have another stomach to fill. Those bastards have no respect for who feeds them every damn day!");
    next;

    menu
        l("So rude. What is the reason behind your malice?"), l_Rude,
        l("I swear, I do not eat so much."), -;

    close;

l_Rude:
    mes "";
    mesq l("UAHAHAHAAH! I am the cook of that bunch of donkeys, I can be rude with everyone from the bossy captain to that harpy-lady!");
    next;

    menu
        l("Harpy-lady?"), -;

    mes "";
    mesq l("I am sure you know her. Nice dress, large smile and a constant flux of orders and rules. Julia they call her!");
    next;
    mesq l("You have to know that there was a time during which I was the unopposed captain of this crew. Then one day one of the damn sailor brought here that nice lady and Nard, her partner in crime.");
    next;
    mesq l("I will not describe you all the details but after 1 month they were ruling the ship and I was sent down here cooking for those traitors!");
    next;
    mesq l("But I swear that one day..ooh my tasty revenge. Hhmm, actually I wonder if.. maybe.. ..");
    next;

    menu
        l("M..Maybe?"), -;

    mes "";
    mesq l("..Welcome onboard cutie! I just realized I need some delicious ingredients to prepare one..one special recipe.");
    next;
    mesq l("I only need 1 @@, 1 @@ and..", getitemlink("PiouLegs"), getitemlink("Croconut"));
    next;
    mesq l("..and 1 @@.", getitemlink("Aquada"));
    next;

    menu
        l("Sure why not?"), OnSure,
        l("Aquada!? I will not help you with your evil plan!"), -;

    close;

OnSure:
    setq ShipQuests_ChefGado, 1;
    mes "";
    mesq l("Well done! Collect all the ingredients and then come back here. You will be rewarded once they have understood who rules this ship!");

    close;

OnQuestAccepted:
    mesn;
    mesq l("Have you collected all the ingredients for my recipe. Special one too?");
    next;

    if (countitem("PiouLegs") > 0 && countitem("Croconut") > 0 && countitem("Aquada") > 0) goto OnCollected;

    menu
        l("Not yet. I will be back soon."), -;

    close;

OnCollected:
    setq ShipQuests_ChefGado, 2;
    delitem "PiouLegs", 1;
    delitem "Croconut", 1;
    delitem "Aquada", 1;
    
    menu
        l("All your..fresh ingredients are ready to be cooked."), -;

    mes "";
    mesq l("Let me see.. Crispy legs, disgusting liquids..let's start.");
    next;
    mesq l("Water, salt, spicy herbs and meat stuffed with my special surprise!");
    next;
    mesq l("Done, take it! Now here it is the plan. Go talk with her and offer our beautiful lady a bite of her arrogancy!");
    next;

    close;

OnPoisonAccepted:
    close;

}
