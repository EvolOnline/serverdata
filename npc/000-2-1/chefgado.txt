// Evol scripts.
// Author:
//    Hal9000
// Description:
//    La Johanne Chef.
// Variable:
//    ShipQuests_ChefGado
//    ShipQuests_Nard
// Values:
//    0   Default, no quest given.
//    1   Quest accepted.
//    2   Ingredients collected, ready to poison Julia.
//    3   Julia poisoned.
//    4   Quest complete.
//    5   Quest aborted. Julia wins.

000-2-1.gat,27,28,0,1	script	Chef Gado	318;2,{

    set @q, getq(ShipQuests_ChefGado);
    set @n, getq(ShipQuests_Nard);
    if (@q == 1) goto l_QuestAccepted;
    if (@q == 2) goto l_PoisonAccepted;
    if (@q == 3) goto l_PoisonJulia;
    if (@q == 4) goto l_QuestComplete;
    if (@q == 5) goto l_QuestAborted;
    if (@n == 3) goto l_QuestStart;

    mesn;
    mesq l("Where is the damn salt?! Give me the salt. I know you have it!");
    close;

l_QuestStart:
    mesn;
    mesq l("So it seems I have another stomach to fill. Those bastards have no respect for who feeds them every damn day!");
    next;

    menu
        l("How rude! What is the reason behind your malice?"), l_Rude,
        l("I swear, I do not eat so much."), -;

    close;

l_Rude:
    mes "";
    mesn;
    mesq l("UAHAHAHAAH! I am the cook of that bunch of donkeys and I can be rude with everyone from this bossy captain to that harpy-lady!");
    next;

    menu
        l("Harpy-lady?"), -;

    mes "";
    mesn;
    mesq l("I am sure you know her. Nice dress, large smile and a constant flux of orders and rules. Julia they call her!");
    next;
    mesq l("You have to know that there was a time during which I was the unopposed lieutenant of this crew. Then one day one of the damn sailor brought here that 'nice' lady.");
    next;
    mesq l("I will not tell you all the details, but after a month, she was ruling the ship and I was sent down here, cooking for those traitors!");
    next;
    mesq l("But I swear that one day... Oh... My tasty revenge! Hhm, actually I wonder if... Maybe...");
    next;

    menu
        l("M... Maybe?"), -;

    mes "";
    mesn;
    mesq lg("Well... Welcome aboard, cutie!");
    next;
    mesq l("I just realized I needed delicious ingredients to prepare a... A special recipe.");
    next;
    mesq l("I only need 2 @@, 1 @@, 1 @@ and...", getitemlink("PiouLegs"), getitemlink("Croconut"), getitemlink("Aquada"));
    next;
    mesq l("... And 1 @@.", getitemlink("SeaDrops"));
    next;

    menu
        l("Sure, why not?"), l_Sure,
        l("Sea Drops!? I will not help you with your evil plan!"), -;

    close;

l_Sure:
    setq ShipQuests_ChefGado, 1;
    mes "";
    mesn;
    mesq l("Well done! Collect all the ingredients and then come back here. You will be rewarded once they understand who rules this ship!");

    close;

l_QuestAccepted:
    mesn;
    mesq l("Have you collected all the ingredients for my recipe? The special one too?");
    next;

    menu
        rif(countitem("PiouLegs") > 1 && countitem("Croconut") > 0 && countitem("Aquada") > 0 && countitem("SeaDrops") > 0, l("All your... Fresh ingredients are ready to be cooked.")), l_GotAll,
        l("What are your needs?"), l_Need,
        l("Not yet. I will be back soon."), -;
    close;

l_GotAll:
    delitem "PiouLegs", 2;
    delitem "Croconut", 1;
    delitem "Aquada", 1;
    delitem "SeaDrops", 1;
    setq ShipQuests_ChefGado, 2;

    mes "";
    mesn;
    mesq l("Let me see... Crispy legs, disgusting liquids... Let's start!");
    next;
    mesq l("Water, salt, spicy herbs and meat stuffed with my special surprise!");
    next;
    mesq l("Done. Here, take it! Now here is the plan. Go talk with her and offer our beautiful lady a bite of her arrogance!");
    getitem "PoisonedDish", 1;

    close;

l_Need:
    mes "";
    mesn;
    mesq l("Shhht, don't say it that loud...");
    next;
    mesq l("I only need 2 @@, 1 @@, 1 @@ and...", getitemlink("PiouLegs"), getitemlink("Croconut"), getitemlink("Aquada"));
    next;
    mesq l("... And 1 @@.", getitemlink("SeaDrops"));

    close;

l_PoisonAccepted:
    mesn;
    mesq l("Fool! Just come back here when you'll be done with our little... 'Secret mission'.");

    close;

l_PoisonJulia:
    mesn;
    mesq l("The usurper has been punished! This is a great day! Take this reward as a prize for your loyalty to the old commander!.");

    setq ShipQuests_ChefGado, 4;

    close;

l_QuestComplete:
    mesn;
    mesq l("Oh, it's you. I think it's better we do not talk for a while. They suspect something.");

    close;

l_QuestAborted:
    mesn;
    mesq lg("You dirty liar. I will add your name on the traitors list.");

    close;

}
