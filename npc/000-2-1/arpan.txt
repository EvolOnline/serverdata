// Evol scripts.
// Authors:
//    Ablu
// Description:
//    Magic Arpan which tells new player how they came here.
//    A box with clothes for every new player.
// Variable:
//    ShipQuests_Arpan
// Values:
//    0: Have not talked to Arpan yet
//    1: Talked to Arpan. Need to get cloths
//    2: Got cloths
//    3: Got cloths + equipped them

000-2-1.gat,39,36,0,1	script	LeftDoorCheck	0,0,0,{
    set @q, geta2(ShipQuests, ShipQuests_Arpan);
    if (@q == 0) doevent "Magic Arpan::OnTalk";

    close;

}

000-2-1.gat,44,36,0,1	script	RightDoorCheck	0,0,0,{
    set @q, geta2(ShipQuests, ShipQuests_Arpan);
    if (@q == 0) doevent "Magic Arpan::OnTalk";

    close;

}

000-2-1.gat,37,32,0,1	script	LeftBarrierCheck	0,0,2,{
    set @q, geta2(ShipQuests, ShipQuests_Arpan);
    if (@q > 2) close;
    if (@q == 2 && getequipid(equip_torso) == 1300 && getequipid(equip_legs) == 2200) goto l_EquipDone;
    if (@q == 1) goto l_GetCloth;

    doevent "Magic Arpan::OnClothNotTaken";
    warp "000-2-1.gat", 39, 32;
    doevent "Magic Arpan::OnEquip";

    close;

l_GetCloth:
    warp "000-2-1.gat", 39, 32;
    doevent "Magic Arpan::OnClothNotTaken";

    close;

l_EquipDone:
    seta2 ShipQuests, ShipQuests_Arpan, 3;

    close;

}

000-2-1.gat,46,32,0,1	script	RightBarrierCheck	0,0,2,{
    set @q, geta2(ShipQuests, ShipQuests_Arpan);
    if (@q > 2) close;
    if (@q == 2 && getequipid(equip_torso) == 1300 && getequipid(equip_legs) == 2200) goto l_EquipDone;
    if (@q == 1) goto l_GetCloth;

    doevent "Magic Arpan::OnClothNotTaken";
    warp "000-2-1.gat", 45, 32;
    doevent "Magic Arpan::OnEquip";

    close;

l_GetCloth:
    warp "000-2-1.gat", 45, 32;
    doevent "Magic Arpan::OnClothNotTaken";

    close;

l_EquipDone:
    seta2 ShipQuests, ShipQuests_Arpan, 3;

    close;

}

000-2-1.gat,41,37,0	script	Box	999,{
    mesn "Narrator";

    set @q, geta2(ShipQuests, ShipQuests_Arpan);
    if (@q == 1) goto l_Give;
    if (@q > 1) goto l_Empty;

    mes col(l("This box is locked"), 9);
    close;

l_Give:
    mes col(l("You see some items in the box. Take them out?"), 9);
    next;

    menu
        l("Yes."), -,
        l("No."), l_Close;

    seta2 ShipQuests, ShipQuests_Arpan, 2;
    getitem "Wornshirt", 1;
    getitem "Wornshort", 1;

l_Close:
    close;

l_Empty:
    mes col(l("This box is locked."), 9);

    close;

}


000-2-1.gat,39,33,0,1	script	Magic Arpan	307,{

OnTalk:
    set @q, geta2(ShipQuests, ShipQuests_Arpan);

    mesn;

    if (@q > 0) goto l_Menu;

    mesq lg("Yeye, you are really lucky to be alive. You are in good enough shape for walking, do you remember what happened?");
    next;
    seta2 ShipQuests, ShipQuests_Arpan, 1;

    menu
        lg("I only remember I was rescued by you."), l_Story,
        lg("I can't remember anything."), l_OhWell;

OnClothNotTaken:
    mesn;
    mesq lg("Yeye, you still did not get your clothes!");

    close;

OnEquip:
    mesn "Narrator";
    mes col(lg("Open your inventory (F3 key), select the clothes one by one and equip them."), 9);

    close;

l_OhWell:
    mes "";
    mesq lg("Oh well, we rescued you when you were yaying adrift in the sea.");

l_Story:
    mes "";
    mesq lg("You were yaying sleeping for quite some days there, our shipkeeper, Julia, was here with you, she did her best to fix your injuries during this time.");
    next;
    mesq lg("Yaya, you should go see her! She willmes be happy to see you.");
    next;
    mesq lg("Also, we took your yayed clothes, as they were... Yeyeye... In bad condition, go check the box around your bed, there is some new ones inside.");
    setcamnpc "Box";
    next;
    restorecam;

l_Menu:
    mesq l("What yeye could I do for you today?");
    next;
    set @equipped, getequipid(equip_torso) == 1300 && getequipid(equip_legs) == 2200;

    menu
        lg("Could you tell me where I am?"), l_Where,
        lg("Where can I find Julia?"), l_Julia,
        l("Who are you?"), l_Who,
        rif(geta(ShipQuests, ShipQuests_ArpanMoney) == 0, l("Where are my old clothes?")), l_WhereOldClothes,
        rif(!@equipped, lg("What should I do after taking these clothes?")), l_WhatCloth,
        rif(!@equipped, l("Thank you, I'll take them."), l("Nothing, sorry.")), l_Quit;
    close;

l_Where:
    mes "";
    mesn;
    mesq lg("You are on our ship, we made port to a little island and we are actually yeyending our long merchant traveling adventure at the city of Artis.");
    next;
    mesq l("We will be yaying there in a few days, so we will drop you off there.");
    next;
    mesq l("You will see, citizens are polite and you can still ask for help in the warrior guild. They can help find a job for you or maybe help you find out what happened to you out at sea!");
    next;

    goto l_Menu;

l_Julia:
    mes "";
    mesn;
    mesq g(l("She is on the upper level, yeye can't miss her. She is the only girl in this crew, oh well, except for you now yeyeye!"),
           l("She is on the upper level, yeye can't miss her. She is the only girl in this crew."));
    next;
    mesq lg("You can go to your right to go to the upper level.");
    next;

    goto l_Menu;

l_Who:
    mes "";
    mesn;
    mesq l("Sorry! I forgot to introduce myself. My name is Arpan, but other sailors call me Magic Arpan because I know one or two yaing magic spells.");
    next;

    goto l_Menu;

l_WhereOldClothes:
    mes "";
    mesn;
    mesq lg("We tried to clean them but the sea water mostly killed them. It's why we gave you these cloths, they are not very good but it's all that we got.");
    next;
    mes lg("Oh, now that I remember, we also find some money on your pockets, here they are!");
    seta ShipQuests, ShipQuests_ArpanMoney, 1;
    set zeny, zeny+25;
    next;

    goto l_Menu;

l_WhatCloth:
    mes "";
    mesn;
    mesq lg("Oh yeyeye, as they are not eatable, you can try to equip them!");
    next;

    mesn "Narrator";
    mes col(lg("Open your inventory (F3 key), select the clothes one by one and equip them."), 9);
    next;

    goto l_Menu;

l_Quit:
    close;

}