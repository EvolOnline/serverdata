// Evol scripts.
// Author:
//    Ablu
// Description:
//    Tells to the player how he got here.
// Variable:
//    ShipQuests_Arpan
// Values:
//    0   Has not talked to Arpan yet.
//    1   Talked to Arpan and needs to get the clothes.
//    2   Has the clothes.
//    3   Has and equipped the clothes.

000-2-1.gat,39,36,0,1	script	LeftDoorCheck	0,0,0,{
    set @q, getq(ShipQuests_Arpan);
    if (@q == 0) doevent "Magic Arpan::OnTalk";

    close;

}

000-2-1.gat,44,36,0,1	script	RightDoorCheck	0,0,0,{
    set @q, getq(ShipQuests_Arpan);
    if (@q == 0) doevent "Magic Arpan::OnTalk";

    close;

}

000-2-1.gat,37,32,0,1	script	LeftBarrierCheck	0,0,2,{
    set @q, getq(ShipQuests_Arpan);
    if (@q > 2) close;
    if (@q == 2 && getequipid(equip_torso) == 1300 && getequipid(equip_legs) == 2200) goto l_EquipDone;
    if (@q == 1) goto l_GetCloth;

    doevent "Magic Arpan::OnClothNotTaken";
    warp "000-2-1.gat", 39, 32;
    doevent "Magic Arpan::OnEquip";

    close;

l_GetCloth:
    warp "000-2-1.gat", 39, 32;
    doevent "Magic Arpan::OnClothNotTaken";

    close;

l_EquipDone:
    setq ShipQuests_Arpan, 3;

    close;

}

000-2-1.gat,46,32,0,1	script	RightBarrierCheck	0,0,2,{
    set @q, getq(ShipQuests_Arpan);
    if (@q > 2) close;
    if (@q == 2 && getequipid(equip_torso) == 1300 && getequipid(equip_legs) == 2200) goto l_EquipDone;
    if (@q == 1) goto l_GetCloth;

    doevent "Magic Arpan::OnClothNotTaken";
    warp "000-2-1.gat", 45, 32;
    doevent "Magic Arpan::OnEquip";

    close;

l_GetCloth:
    warp "000-2-1.gat", 45, 32;
    doevent "Magic Arpan::OnClothNotTaken";

    close;

l_EquipDone:
    setq ShipQuests_Arpan, 3;

    close;

}

000-2-1.gat,39,33,0,1	script	Magic Arpan	307;5,{

    set @q, getq(ShipQuests_Arpan);
    if (@q == 2 && getequipid(equip_torso) == 1300 && getequipid(equip_legs) == 2200) goto l_EquipDone;

OnTalk:
    mesn;

    if (@q > 0) goto l_Menu;

    mesq lg("Yeye, you are really lucky to be alive. You are in good enough shape for walking, do you remember what happened?");
    next;
    setq ShipQuests_Arpan, 1;

    menu
        lg("I only remember I was rescued by you."), l_Story,
        lg("I can't remember anything."), l_OhWell;

OnClothNotTaken:
    mesn;
    mesq lg("Yeye, you still did not get your clothes!");

    close;

OnEquip:
    mesn "Narrator";
    mes col(lg("If you would like to open your inventory use the F3 key or use your mouse to select it in the above menu in your client."), 9);
    next;
    mes col(lg("Once you're inside of your inventory, you may equip the item by selecting it and clicking 'Equip'. Alternately, you can unequip an item by selecting 'unequip' to remove it."), 9);
    next;
    mes col(lg("Certain items perform different effects. Some will heal you, some you may use as weapons or armor, and some can be sold for gold."), 9);

    close;

l_OhWell:
    mes "";
    mesq lg("Oh well, we rescued you when you were yaying adrift in the sea.");

l_Story:
    mes "";
    mesq lg("You were yaying sleeping for quite some time there, our shipkeeper, Julia, was here with you, she did her best to heal your injuries.");
    next;
    mesq lg("Yaya, you should go see her! She'll be happy to see you.");
    next;
    mesq lg("Also, we took your yayed clothes, as they were... Yeyeye... In a bad shape. Go check the chest near your bed, there are some other ones inside.");
    setcamnpc "Chest";
    next;
    restorecam;

l_Menu:
    mesq l("What yeye could I do for you today?");
    next;
    set @equipped, getequipid(equip_torso) == 1300 && getequipid(equip_legs) == 2200;

    menu
        lg("Could you tell me where I am?"), l_Where,
        lg("Where can I find Julia?"), l_Julia,
        l("Who are you?"), l_Who,
        rif(getq(ShipQuests_ArpanMoney) == 0, l("Where are my old clothes?")), l_WhereOldClothes,
        rif(!@equipped, lg("What should I do after taking these clothes?")), l_WhatCloth,
        rif(!@equipped, l("Thank you, I'll take them."), l("Nothing, sorry.")), l_Quit;
    close;

l_Where:
    mes "";
    mesn;
    mesq lg("You're on our ship, we made port to a little island and we're actually yeyending our long merchant traveling adventure at the city of Artis.");
    next;
    mesq l("We will be yaying there in a few days, so we will drop you off there.");
    next;
    mesq l("You will see, citizens are polite and you can still ask for help in the warrior guild. They can help find a job for you or maybe help you find out what happened to you out at sea!");
    next;

    goto l_Menu;

l_Julia:
    mes "";
    mesn;
    mesq g(l("She's on the upper level, yeye can't miss her. She's the only girl in this crew, oh well, except for you now yeyeye!"),
           l("She's on the upper level, yeye can't miss her. She's the only girl in this crew."));
    next;

    mesn "Narrator";
    mes col(l("Julia is on the upper level of the ship, use the arrow keys to walk to the stairs or click on the stairs at the top right of your screen."), 9);
    next;

    goto l_Menu;

l_Who:
    mes "";
    mesn;
    mesq l("Sorry! I forgot to introduce myself. My name is Arpan, but other sailors call me Magic Arpan because I know one or two yaing magic spells.");
    next;

    goto l_Menu;

l_WhereOldClothes:
    mes "";
    mesn;
    mesq lg("We tried to clean them but the sea water mostly destroyed them. It's why we gave you these clothes. They're not very nice, but that's all that we have for you.");
    next;
    mes lg("Oh, now that I remember, we've also found some money in your pockets, here it is!");
    next;

    setq ShipQuests_ArpanMoney, 1;
    set zeny, zeny+25;
    message strcharinfo(0), l("You receive 25GP!");

    goto l_BeforeMenu;

l_WhatCloth:
    mes "";
    mesn;
    mesq lg("Oh yeyeye... As they are not edible, you can maybe try to equip them?");
    next;

    mesn "Narrator";
    mes col(lg("To open your inventory, use the F3 key or use your mouse in the above menu of your client."), 9);
    next;
    mes col(lg("When your inventory will be opened, you may equip the item by selecting it and clicking 'Equip'. Alternately, you can unequip an item by selecting 'Unequip' to remove it."), 9);
    next;
    mes col(lg("Items perform different effects. Some will heal you, some you may use as weapons or armor and some can be sold for gold."), 9);
    next;

l_BeforeMenu:
    mesn;
    goto l_Menu;

l_EquipDone:
    setq ShipQuests_Arpan, 3;
    goto l_Menu;

l_Quit:
    close;

}
