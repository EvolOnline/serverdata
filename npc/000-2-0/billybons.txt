// Evol scripts.
// Author:
//    Reid
// Description:
//    Drunk easteregg telling about the player's past.

000-2-0.gat,20,23,0,1	script	Billy Bons	311;3,{

    if (rand(5) == 2) goto l_Hic;

    mesn;
    mesq l("You? Here?");
    mesq l("How is *hic* it possible?");
    next;

    mesn "Narrator";
    mes col(l("The sailor chugs his beer."), 9);
    next;

    menu
        lg("Excuse me? Do you know who I am?"), -;

    mes "";
    mesn;
    mesq l("Don't do theee... *hic* with me eh!");
    next;

    menu
        l("Are you ok?"), l_Quit,
        l("Alright... Bye."), -;

    mes "";
    mesn;
    mesq lg("No and *hic*... No, you and you and your... *burp* stupi*hic* guild!");
    next;
    mesq l("You tried to get rid of me, eeh? But surprise! I'm still here... *hic* Or there...");
    next;
    mesq l("But you won't *hic* me this time...");
    next;

    menu
        l("What are you talking about? What guild?"), l_Quit,
        l("You are full of wine, my friend..."), -,
        l("You should go and get some sleep."), l_Quit;

    mes "";
    mesn;
    mesq l("If I saw *hic* who you were... *hic* Would not have helped you! ");
    next;

    menu
        l("But who am I?"), -,
        l("What am I supposed to say?"), l_Quit,
        l("You should go and get some sleep."), l_Secret;

    mes "";
    mesn;
    mesq l("The giant boogeyman!");

    close;

l_Secret:
    mes "";
    mesn;
    mesq l("Hear me *hic* well, what ever, whatididever you will *hic* said ab... uhm... out what?! You saw there, the Warrior Guild of Esperia won't let it get public.");
    next;
    mesq l("Nobody will know about the existence of the Mercurians.");
    next;
    mesq l("Nobody! *burp*");
    next;

    close;

l_Hic:
    npctalk3 "*hic*";

    close;

l_Quit:
    mes "";
    mesn;
    mesq l("Yeah you're all like *hic* that in Esperia, but you won't get me! *burp*");
    next;
    mesq l("I'm not that numb eeh *hic* what did ever you disco... *hic* ...vered there, the Warrior Guild won't get me!");
    next;

    mesn "Narrator";
    mes col(l("The sailor turns his back to you."), 9);

    close;
}
