// Evol scripts.
// Authors:
//    4144
//    Vasily_Makarov
// Description:
//    Allows to change language and talks about what happened to him.
//    2 Bits Array:
//    ShipQuests
// Variables:
//    0 ShipQuests_Julia
//    1 ShipQuests_ChefGado
// Values ShipQuests_Julia:
//    00   Has not talked to Julia
//    01   Need to see Julia
//    02   Has been registered by Julia
// Values ShipQuests_ChefGado:
//    12   Ingredients collected, ready to poison Julia.
//    13   Julia poisoned.
//    15   Quest aborted.

000-2-0.gat,27,24,0,0	script	Julia	304,2,10;5,{

    mesn;
    mesq lg("Hello dear!");
    next;
    mesq l("What do you want today?");
    next;

    set @q2, getq(ShipQuests_ChefGado);
    if (@q2 == 2 && countitem("PoisonedDish") > 0) goto l_PoisonJulia;

  set @s$, l("I don't want to change my language, sorry.");

l_Menu:
    menu
        lg("I made a mistake, I would like to change my language."), l_ChooseLang,
        l("Could you explain to me where I am?"), l_WhereIam,
        l("What happened to me?"), l_What,
        l("Can I read these rules again?"), l_Rules,
        l("Nothing, sorry."), l_Close;

    mes "";
    mesn;
    mesq l("Of course! Tell me which language you speak and I will change the note on the ship board list.");

    goto l_ChooseLang;

l_First:

    callfunc "CheckClientVersion";

    mesn;
    mesq l("Hi, nice to see you!");
    next;
    mesq l("My name is Julia, it is me who took care of you after we found you in the sea.");
    next;
    mesq lg("I'm glad to see you're okay.");
    next;
    mesq lg("Could I ask you what your native language is? A sailor told me you're russian, but another one told me you're french... I'm a bit lost. I will register you on the ship boarding list just after that.");
    next;
    set @s$, "";
    goto l_ChooseLang;

  l_ChooseLang:
    menu
        l("I speak English.") + " (English)", -,
        l("I speak French.") + " (Français)", -,
        l("I speak Russian.") + " (Русский)", -,
        l("I speak Spanish.") + " (Español)", -,
        l("I speak Portuguese.") + " (Português)", -,
        l("I speak German.") + " (Deutsch)", -,
        l("I speak Flemish.") + " (Vloams)", -,
        l("I speak Polish.") + " (Polski)", -,
        l("I speak Italian.") + " (Italian)", -,
        l("I speak Dutch.") + " (Nederlands)", -,
    @s$, l_NoChanges;
    set @lang, @menu-1;
    if (@lang < 0 || @lang > 9) close;
    setlang @lang;

    mes "";
    mesn;
    mesq l("Ok, done.");

    set @q, getq(ShipQuests_Julia);
    if (@q == 2) goto l_NoRules;

    next;
    mesq l("I'm sure that you've got some questions for me, feel free to ask them, but firstly I need to tell you the rules of proper social conduct on board.");
    
    mesq l("Here they are.");
    next;

    mesn "Narrator";
    mes col(l("There is a paper with some rules written on it."), 9);
    next;
    mesq col(l("1. Do not bot. This means you are not allowed to use any automation programs or use keyboard tricks. Any away from keyboard activity will be considered as botting except standing still."), 9);
    next;
    mesq col(l("2. Do not spam. This also concerns trade spam."), 9);
    next;
    mesq col(l("3. Do not multi box. This means you cannot have more than one active character logged in at a time. You are not allowed to use more than one active client at a time to attack in groups."), 9);
    next;
    mesq col(l("4. Do not trash talk, beg or use rude language in your character name or in the chat, except for role play purposes."), 9);
    next;
    mesq col(l("5. Do not speak any other language than English in public. You are free to speak any language you want in private chats or when alone with friends."), 9);
    next;
    mesq col(l("6. Follow the social convention listed on the RFC1855 article."), 9);
    next;

    mesn;
    mesq l("Oh, and I almost forgot! Do not give the password of your room to anybody! I am the only one who has the other key and I won't ask for yours so keep it secret and try not to use the same password for any other room in the future.");
    next;
    mesq l("If you want to read this page again, there is a copy up on the left wall.");
    next;
    mesq l("I think I'm done with that now. Do you have any questions?");
    next;

    setq ShipQuests_Julia, 2;
    goto l_Menu;

l_NoRules:
    next;
    mesq l("Do you have any other questions for me?");
    next;

    goto l_YNMenu;

l_NoChanges:
    mes "";
    mesn;
    mesq l("No problem, do you have any other questions for me?");
    next;

l_YNMenu:
    menu
        l("Yes, I do."), l_Menu,
        l("No, none."), l_Close;

l_WhereIam:
    mes "";
    mesn;
    mesq l("You're on a ship, we're on our way to the commercial capital of Artis.");
    next;
    mesq l("We should be there in a few days, once we arrive, I will warn the warrior guild about what happened, I'm sure they can help.");
    next;
    mesq l("But for now, you can relax on the ship, or visit the island we're docked at! Its a small island, but a good place to get some exercise and stretch your legs.");
    next;
    mesq l("Do you have any other questions for me?");
    next;

    goto l_YNMenu;

l_What:
    mes "";
    mesn;
    mesq l("We thought that you could help us understand this, all we know is that we found you back in the sea, adrift on your raft.");
    next;
    mesq lg("You were in bad shape, you should be happy we found you before the sea killed you.");
    next;
    mesq l("Oh, and there was this inscription on your raft. It represents one of the warrior guild of Esperia, the largest and biggest guild of the whole new world. Does that make you remember anything, anything at all?");
    next;
    goto l_EsperiaMenu;

l_EsperiaMenu:
    menu
        l("Sorry, but I can't tell you anything about that."), l_NoChanges,
        l("Nothing, sorry."), l_NoChanges;

l_Close:
    mes "";
    mesn;
    mesq l("Bye!");

    close;

l_Rules:
    mes "";
    mesn;
    mesq l("Of course, they are on the left wall, go have a look at them.");
    next;
    mesq l("Do you have any other questions for me?");
    next;

    goto l_YNMenu;

OnTouch:
    set @q, getq(ShipQuests_Julia);
    if (@q == 0 || @q == 1) goto l_First;

    close;

l_PoisonJulia:
    menu
        l("I have brought you a tasty present for your delicate mouth."), -,
        l("Well... No wait, I have something for you but you shouldn't eat it... I'm taking it back to the kitchen."), l_Regret;

    mes "";
    mesq l("Seems yummy! Let me taste it!");
    next;
    mesq l("Hmmm, hm... *cough*, *cough*, *burp*, *cough*. What... What is that?!... *cough*, *burp*... Damn Gado... *cough*");
    if (countitem("PoisonedDish") > 0), delitem "PoisonedDish", 1;
    setq ShipQuests_ChefGado, 3;

    close;

l_Regret:
    mes "";
    mesq l("Why? And who should you bring it back to?");
    next;

    menu
        l("Ehm... He was really upset for some past stories."), -;

    mes "";
    mesq l("Gado! That coward lives in the past, I will ask Nard to punish him, again!");
    next;
    mesq l("Take this money for your wise choice. But do not try it again. Water has been merciful with you once... Do not tempt fate once more!");
    setq ShipQuests_ChefGado, 5;

    close;

}

