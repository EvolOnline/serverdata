// Evol scripts.
// Authors:
//    Vasily_Makarov
//    4144
// Description:
//    Allow change language and some speaking
//
// 2 bits Array:
//    ShipQuests
// Variable:
//    ShipQuests_Julia
// Values:
//    0     first touch
//    1     already touched

000-2-0.gat,27,24,0,0	script	Julia	304,2,10,{
  mesn;
  mesq g(l("Hello, girl!"),
         l("Hello, boy!"));
  mesq l("What do you want today?");
  set @s$, l("I don't want to change my language, sorry.");
  l_Menu:
  menu
    lg("I made a mistake, I would like to change my language."), l_ChooseLang,
    l("Could you explain to me where I am?"), l_WhereIam,
    l("What happened to me?"), l_What,
    l("Nothing, sorry."), l_Close;
    mesn;
    mesq l("Of course! Tell me which language you speak and I will change the note on the ship board list.");
    goto l_ChooseLang;

  l_First:
    seta2 ShipQuests, ShipQuests_Julia, 1;
    mesn;
    mesq l("Hi, nice to see you!");
    next;
    mesq l("I'm called Julia, it's me who took care of you some days ago, when we found you back on the sea, I'm very happy to see that you seem okay now!");
    next;
    mesq l("Could I ask you what your native language is? A sailor told me you're russian, but another one told me you're french... I'm a bit lost. I will register you on the ship boarding list just after that.");
    set @s$, "";
    goto l_ChooseLang;

  l_ChooseLang:
    menu
      l("I speak English") + " (English)", -,
      l("I speak French") + " (Français)", -,
      l("I speak Russian") + " (Русский)", -,
      l("I speak Spanish") + " (Español)", -,
      l("I speak Flemish") + " (West-Vloams)", -,
      l("I speak German") + " (Deutsch)", -,
      l("I speak Dutch") + " (Nederlands)", -,
      l("I speak Italian") + " (Italiano)", -,

      @s$, l_NoChanges;
    set @lang, @menu-1;
    if (@lang < 0 || @lang > 7) close;
    setlang @lang;
    mesn;
    mesq l("Ok, Done. I'm sure that you've got some questions for me, feel free to ask them!");
    goto l_Menu;
    close;

  l_OtherQuestion:
    mesn;
    mesq l("Do you have an other question for me?");
    goto l_YNMenu;
    close;

  l_NoChanges:
    mesn;
    mesq l("No problem, do you have an other question for me?");

  l_YNMenu:
    menu
      l("Yes, please!"), l_Menu,
      l("No, thanks."), -;
    close;

  l_WhereIam:
    mesn;
    mesq l("You are actually on a ship, we're on our way to the commercial capital of Artis. ");
    next;
    mesq l("We should be there in a few days, and once we arrive, I will advertise the warrior guild of what happened, I'm sure that they can help you.");
    next;
    mesq l("But until then, you need to stay here, there is nothing else to do anyway.");
    next;
    goto l_OtherQuestion;

  l_What:
    mesn;
    mesq l("We thought that you could help us understand this, all we know is that we found you back in the sea, adrift on your raft.");
    next;
    mesq l("You were in a bad mood and you should be happy that we found you before the sea took you.");
    next;
    mesq l("Also, there was this inscription on your raft, one of the warrior guild of Esperia, the largest and biggest guild of the whole new world, does that make you remember something?");
    next;
    goto l_EsperiaMenu;

  l_EsperiaMenu:
  menu
    l("Sorry but I can't tell you anything about that."), l_NoChanges,
    l("Nothing, sorry."), l_NoChanges;

  l_Close:
    close;

  OnTouch:
    set @q, geta2(ShipQuests, ShipQuests_Julia);
    if (@q == 0) goto l_First;
    close;
}
