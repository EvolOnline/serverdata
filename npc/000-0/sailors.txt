// Evol scripts.
// Authors:
//    Vasily_Makarov
//    Reid
// Description:	
//    Start script of Evol Online.

000-0.gat,23,20,0,1	script	Sailors	304,6,6,{

OnTouch:
  mesn "Narrator";
  mes "##BYou are on a raft, adrift in the sea.##b";
  next;
  mes "##BYou don't remember anything before this.##b";
  next;
  mes "##BSuddenly, you hear a voice from the sky.##b";
  next;
  mes "##BAs you open your eyes and look around, you see a large ship.##b";
  next;
  mes "##BSome sailors are trying to talk to you..##b";
  next;

  mesn "Orc Voice";
  mesq g("Hey Frenchy!#0", "Hey Frenchy!#1");
  next;

  mesn "Elfen Voice";
  mesq g("Why Rrenchy? It's a Russian!#0", "Why Frenchy? It's a Russian!#1");
  next;

  mesn "Tritan Voice";
  mesq "What do you say guys, it's a yoiis!";
  next;

  mesn "Raijin Voice";
  mesq g("You stupid, it's an english, look her head form.", "You stupid, it's an english, look his head form.");
  next;
  mesq "Hey you! Do you hear us? Are you okay?";
  next;

  menu
    "I hear you..." + " (English)", -,
    "Je vous entends..." + " (Français)", -,
    "Да я вас слышу..." + " (Русский)", -,
    "Te oigo..." + " (Español)", -,
    "'k hoar ui wel..." + " (West-Vloams)", -,
    "I höre euch..." + " (Deutsch)", -,
    "Ik hoor je..." + " (Nederlands)", -,
    "Ti sento..." + " (Italiano)", -;
    set @lang, @menu-1;
    if (@lang < 0 || @lang > 7) close;
    setlang @lang;

  mesn;
  mesq l("Oh, ") + g(l("she's still alive!"), l("he's still alive!"));
  next;
  mesq g(l("This girl needs help, let's rescue her!"), l("This guy needs help, let's rescue him!"));
  next;

  mesn l("##BNarrator##b");
  mesq l("##BThe sailors take you aboard their ship to help you.##b");
  next;

  mesn l("##BNarrator##b");
  mesq l("##BClick on the NPCs around you to continue the introduction.##b");
  next;

  warp "000-0-0.gat",22,22;

  close;

}
