// Evol scripts.
// Authors:
//    4144
//    Reid
//    Vasily_Makarov
// Description:
//    Starting script of Evol Online.

000-0.gat,23,20,0,1	script	Sailors	305,6,6,{

OnTouch:
    callfunc "CheckClientVersion";
    requestlang @lang;
    if (@lang >= 0 && @lang <= 7) setlang @lang;

    mesn "Narrator";
    mes col(l("You are on a raft, adrift in the sea."), 9);
    next;
    mes col(l("You realize you can't remember anything."), 9);
    next;
    mes col(l("Suddenly, you hear a voice from the sky."), 9);
    next;
    mes col(l("As you open your eyes, you see a large ship before you."), 9);
    next;
    mes col(l("Aboard stand sailors trying to talk to you."), 9);
    next;

    mesn "Orc Voice";
    mesq lg("Hey Frenchy!");
    next;

    mesn "Elven Voice";
    mesq lg("Why Frenchy? It's a Russian!");
    next;

    mesn "Tritan Voice";
    mesq lg("What are you guys saying ? It's a Yoiis!");
    next;

    mesn "Raijin Voice";
    mesq g(l("You stupid, she's english, look at the shape of the head."),
           l("You stupid, he's english, look at the shape of the head."));
    next;
    mesq l("Hey you! Can you hear us? Are you okay?");
    next;

    menu
        "I hear you..." + " (English)", -,
        "Je vous entends..." + " (Français)", -,
        "Да я вас слышу..." + " (Русский)", -,
        "Te oigo..." + " (Español)", -,
        "Eu te ouço..." + " (Português)", -,
        "I höre euch..." + " (Deutsch)", -,
        "'k hoar ui wel..." + " (Vloams)", -,
        "Słyszę cię..." + " (Polish)", -,
        "I hear you..." + " (Italian)", -,
        "Ik kan je horen..." + " (Nederlands)", -;
    set @lang, @menu-1;
    if (@lang < 0 || @lang > 9) close;
    setlang @lang;

    mesn;
    mesq g(l("Oh, she's still alive!"),
           l("Oh, he's still alive!"));
    next;
    mesq g(l("This girl needs help, we need to rescue her!"),
           l("This guy needs help, we need to rescue him!"));
    next;

    mesn "Narrator";
    mes col(l("The sailors take you aboard their ship."), 9);
    next;
    mes col(l("Click on the NPCs around you to continue the introduction."), 9);
    next;

    warp "000-0-0.gat",22,22;

    close;

}
